### Bill Layout Script
### Flag Find Layout
### Language it_IT

### Box Nome fornitore
### Flags NOREAD
### Page 1
### Rect 0.04808817 0.0386214 0.26773414 0.08643837
### Script
fornitore="MetaEnergia";
### End Script
### End Box

### Box Convalida Layout
### Page 1
### Rect 0.062384654 0.31081033 0.7538146 0.026667157
### Flags TRIM
### Script
if(@ != "FATTURA PER LA FORNITURA DI ENERGIA ELETTRICA - MERCATO LIBERO") {
  error("MetaEnergia: Stringa di Convalida Errata", global ERRORE_LAYOUT_INVALIDO);
}
### End Script
### End Box

### Box Dati fornitura
### Mode LAYOUT
### Page 1
### Rect 0.058485612 0.14988782 0.33401784 0.1416118
### Script
ragione_sociale=totitle(search(@,/INTESTATARIO\n(.+)\n/));
indirizzo_fornitura=totitle(search(@,/Le stiamo fornendo energia elettrica in:\n(.+)\n/));
codice_pod=search(@,/Codice POD: (.+)\n/);
### End Script
### End Box

### Box Numero fattura
### Page 1
### Rect 0.06368434 0.33563837 0.87104565 0.02544105
### Script
numero_fattura=search(@,/Fattura N\. (\w+)/);
data_fattura=search_date(@,"%d/%m/%Y",/Emessa il (\D)/);
mese_fattura = search_month(@,"%d/%m/%Y",/Periodo di riferimento \D - (\D)/);
if (!mese_fattura) {
  error("MetaEnergia: Mese Fattura Mancante", global ERRORE_PERIODO_INVALIDO);
}

$mese = date_format(mese_fattura,"%m/%Y");
### End Script
### End Box

### Box Data scadenza
### Page 1
### Rect 0.06368434 0.36414462 0.4483897 0.03586273
### Script
data_scadenza=search_date(@,"%d/%m/%Y",/Totale da pagare entro il (\D)/);
### End Script
### End Box

### Box Totale fattura
### Page 1
### Rect 0.5484651 0.3650642 0.36650985 0.03586273
### Script
totale_fattura = +@;
### End Script
### End Box

### Box Codice cliente
### Mode LAYOUT
### Page 1
### Rect 0.525949 0.045455948 0.31360933 0.07011002
### Script
numero_cliente=search(@,/Codice cliente: (.+)\n/);
### End Script
### End Box

### Box Lettura Dettaglio
### Mode LAYOUT
### Page 2
### Rect 0.052041367 0.12135033 0.9031695 0.8370542
### Goto Label lettura_dettaglio
### Spacers
p+$next_page;
### End Spacers
### Script
if (!(ate() || contains(@,"Comunicazioni finali"))) {
  $dettaglio+=@;
  $next_page++;
  goto lettura_dettaglio;
}
### End Script
### End Box

### Box Parsing Dettaglio
### Flags NOREAD
### Page 2
### Rect 0.3080384 0.08228446 0.04617861 0.029602662
### Script
with($dettaglio) {
  energia_attiva_rilevata=captures_num(search(@,/Energia attiva \(kWh\)\n((Dal.+\n)+)/), format(/al \d\d\/$0 \w+ (\N) (\N) (\N)/, $mese));
  energia_reattiva=captures_num(search(@,/Energia reattiva \(kVARh\)\n((Dal.+\n)+)/), format(/al \d\d\/$0 \w+ (\N) (\N) (\N)/, $mese));

  spesa_materia_energia=search_num(@,/TOTALE SERVIZI DI VENDITA (\N)/);

  tie(prezzo_energia[0], energia_attiva[0])
    = captures_num(search(@,/Energia in Peak\n((Dal.+\n)+)/), format(/al \d\d\/$0 \u20ac\/kWh (\N) (\N)/, $mese));
  tie(prezzo_energia[1], energia_attiva[1])
    = captures_num(search(@,/Energia in Off-Peak\n((Dal.+\n)+)/), format(/al \d\d\/$0 \u20ac\/kWh (\N) (\N)/, $mese));

  trasporto_gestione=search_num(@,/TOTALE SERVIZI DI RETE (\N)/);
  potenza=repeated(search_num(search(@,/Quota Potenza\n((Dal.+\n)+)/),
    format(/al \d\d\/$0 \u20ac\/kW \N (\N)/, $mese)), 3);
  foreach(matches(@,/Energia Reattiva F[1-3] 75%-100%\n((Dal.+\n)+)/)) {
    penale_reattiva_inf75+=search_num(@,format(/al \d\d\/$0 \u20ac\/kVARh \N \N (\N)/, $mese));
  }
  foreach(matches(@,/Energia Reattiva F[1-3] 33%-75%\n((Dal.+\n)+)/)) {
    penale_reattiva_sup75+=search_num(@,format(/al \d\d\/$0 \u20ac\/kVARh \N \N (\N)/, $mese));
  }
  accise=search_num(search(@,/IMPOSTE SUI CONSUMI\nAccisa\n((Dal.+\n)+)/),
    format(/al \d\d\/$0 \u20ac\/kWh \N \N (\N)/, $mese));
  cts=sum(matches_num(search(@,/Corrispettivo Tariffario Specifico.+\n((Dal.+\n)+)/),/\u20ac \N \N (\N)/));
}
### End Script
### End Box

### Box Imponibile
### Page 1
### Rect 0.5064516 0.53105414 0.45645162 0.18005697
### Script
foreach(matches(@,/(IVA \d+% su imponibile di \u20ac .+)/)) {
  $imp=search_num(@,/imponibile di \u20ac (\N)/);
  if($imp > imponibile) {
    imponibile = $imp;
    iva=search(@,/IVA (\d+%)/);
  }
}
### End Script
### End Box
