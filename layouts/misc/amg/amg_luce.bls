### Bill Layout Script
### Flag Find Layout
### Language it_IT

### Box Ricerca Dettaglio
### Flags PAGE
### Page 4
### Rect 0.01455026455026455 0.01216089803554724 0.03835978835978836 0.028999064546304958
### Goto Label ricerca_dettaglio
### Spacers
p+$off_dettaglio;
### End Spacers
### Script
if(ate()) {
  error("AMG: Dettaglio Mancante", global ERRORE_DETTAGLIO_MANCANTE);
} else if(contains(@,"DETTAGLIO FATTURA")) {
  goto lettura_dettaglio;
} else {
  $off_dettaglio++;
  goto ricerca_dettaglio;
}
### End Script
### End Box

### Box Lettura Dettaglio
### Mode LAYOUT
### Flags PAGE
### Page 4
### Rect 0.06084656084656084 0.013096351730589336 0.03835978835978836 0.02806361085126286
### Goto Label lettura_dettaglio
### Spacers
p+$off_dettaglio;
### End Spacers
### Script
if(!ate() && !isempty(@)) {
  $dettaglio+=@;
  $off_dettaglio++;
  goto lettura_dettaglio;
}
### End Script
### End Box

### Box Nome Fornitore
### Flags NOREAD 
### Page 1
### Rect 0.014516129032258065 0.014823261117445839 0.3193548387096774 0.08437856328392247
### Goto Label nome_fornitore
### Script
fornitore="AMG";
### End Script
### End Box

### Box Dati Fornitura
### Mode LAYOUT
### Page 1
### Rect 0.5983870967741935 0.011402508551881421 0.382258064516129 0.10347352450596982
### Script
numero_cliente=search(@,/Codice Anagrafico (\d+)/);
codice_pod=search(@,/POD (\w+)/);
### End Script
### End Box

### Box Ragione Sociale
### Page 1
### Rect 0.5112903225806451 0.18015963511972635 0.4580645161290323 0.09236031927023944
### Script
ragione_sociale=totitle(search(@,/Egr\.\/Spett\.le (.+)\n/));
### End Script
### End Box

### Box Dati Fattura
### Page 1
### Rect 0.509200879765396 0.2862029646522235 0.4612903225806452 0.231869324504403
### Script
totale_fattura=search_num(@,/Importo Fattura \u20ac (\N)/);
data_scadenza=search_date(@,"%d/%m/%Y",/Da pagare entro il (\D)/);
numero_fattura=search(@,/Fattura nr\. (\d+)/);
data_fattura=search_date(@,"%d/%m/%Y",/Fattura nr\. \d+ del (\D)/);
iva=search(@,/Aliquota al (\d+%)/);
### End Script
### End Box

### Box Indirizzo Fornitura
### Mode RAW
### Page 1
### Rect 0.01818181818181818 0.721285140562249 0.49318181818181817 0.21204819277108433
### Script
indirizzo_fornitura=totitle(singleline(search(@,/Indirizzo Fornitura ([^]+\d{5}.+)/)));
### End Script
### End Box

### Box Potenza Disponibile
### Mode LAYOUT
### Page 1
### Rect 0.5125 0.721285140562249 0.4602272727272728 0.21124497991967872
### Script
potenza=repeated(search_num(@,/Potenza Disponibile (\N) kW/), 3);
### End Script
### End Box

### Box Periodo
### Flags TRIM 
### Page 1
### Rect 0.020737327188940113 0.3651181744091279 0.46889400921658986 0.030154849225753844
### Script
if(ismatch(@,/\w+\. - \w+\. \d{4}/)) {
  with(captures_num(@,/(\w+)\. - (\w+)\. (\d{4})/)) {
    $mese_inizio=date(format("$0 $1", @[0], @[2]), "%b %Y");
    $mese_fine=date(format("$0 $1", @[1], @[2]), "%b %Y");
  }
  mese_fattura=month_add($mese_inizio, $calc_mese);
} else {
  error("AMG: Formato Periodo Errato", global ERRORE_PERIODO_INVALIDO);
}
if($mese_inizio > $mese_fine) {
  error("AMG: Formato Periodo Errato", global ERRORE_PERIODO_INVALIDO);
}
$mese=date_format(mese_fattura,"%m/%Y");
### End Script
### End Box

### Box Parsing Lettura
### Mode LAYOUT
### Flags PAGE
### Page 2
### Rect 0.035227272727272725 0.10522088353413654 0.03181818181818182 0.02570281124497993
### Script
with(between(@,"DETTAGLIO LETTURE","DETTAGLIO ALTRE PARTITE")) {
  foreach(matches(@,format(/\d\d\/$0 (.+)/, $mese))) {
    energia_attiva_rilevata=captures_num(@,/(\N) (\N) (\N) Effettivi/);
  }
}
### End Script
### End Box

### Box Parsing Dettaglio
### Flags NOREAD 
### Page 4
### Rect 0.10449735449735449 0.01216089803554724 0.041005291005291 0.02806361085126286
### Script
function check_periodo(@) {
  return date_between(mese_fattura,
    search_date(@,"%d/%m/%Y",/(\D) - \D/),
    search_date(@,"%d/%m/%Y",/\D - (\D)/));
}
function calc_parziale(@) {
  foreach(matches(@,format(/($0 $1 \u20ac\/cliente\/giorni $1 giorni $1)/, $regex_periodo, number_regex()))) {
    if(check_periodo(@)) {
      $ret+=search_num(@,/(\N)$/);
    }
  }
  foreach(matches(@,format(/($0 $1) \u20ac\/cliente\/mese/, $regex_periodo, number_regex()))) {
    if(check_periodo(@)) {
      $ret+=search_num(@,/(\N)$/);
    }
  }
  foreach(matches(@,format(/($0 $1 \u20ac\/kWh $1 kWh $1)/, $regex_periodo, number_regex()))) {
    if(check_periodo(@)) {
      $ret+=search_num(@,/(\N)$/);
    }
  }
  return trunc($ret,2);
}
$regex_periodo=/\d\d\/\d\d\/\d\d\d\d - \d\d\/\d\d\/\d\d\d\d/;
with($dettaglio) {
  with(between(@,"Spesa per la Materia Prima Energia","Spesa per il trasporto")) {
    foreach(matches(@,format(/Comp\. PCV ($0 .+) \u20ac\/cliente\/mese/, $regex_periodo))) {
      if(check_periodo(@)) {
        pcv=search_num(@,/(\N)$/);
        break;
      }
    }
    foreach(matches(@,format(/Comp\. PED F123 ($0 .+ \u20ac\/kWh .+) kWh/, $regex_periodo))) {
      if(check_periodo(@)) {
        with(captures_num(@,/(\N) \u20ac\/kWh (\N)/)) {
          prezzo_energia = @[0];
          energia_attiva += @[1];
        }
      }
    }
    spesa_materia_energia+=calc_parziale(@);
  }
  with(between(@,"Spesa per il trasporto","Spesa per Oneri")) {
    foreach(matches(@,format(/($0 .+ \u20ac\/kW\/mese .+ kW)/, $regex_periodo))) {
      if(check_periodo(@)) {
        potenza=repeated(search_num(@,/(\N) kW/), 3);
        trasporto_gestione+=trunc(search_num(@,/(\N) \u20ac\/kW\/mese/) * potenza,2);
      }
    }
    foreach(matches(@,format(/($0 .+ \u20ac\/kW\/giorni .+ kW\/giorni .+)/, $regex_periodo))) {
      if(check_periodo(@)) {
        potenza=repeated(search_num(@,/(\N) kW/), 3);
        trasporto_gestione+=search_num(@,/giorni (\N)/);
      }
    }
    trasporto_gestione+=calc_parziale(@);
  }
  with(between(@,"Spesa per Oneri","Imposte")) {
    oneri=calc_parziale(@);
  }
  with(between(@,"Imposte","ALTRI IMPORTI")) {
    foreach(matches(@,format(/Imposta Erariale ($0 .+ \u20ac\/kWh .+ kWh .+)/, $regex_periodo))) {
      if(check_periodo(@)) {
        accise=search_num(@,/ kWh (\N)/);
      }
    }
  }
  imponibile = spesa_materia_energia + trasporto_gestione + oneri + accise + altre_partite;
}
### End Script
### End Box

### Box Prossima Pagina
### Flags NOREAD 
### Page 4
### Rect 0.15343915343915343 0.014967259120673527 0.035714285714285726 0.025257249766136577
### Script
if(mese_fattura != $mese_fine) {
  $calc_mese++;
  nexttable();
  goto nome_fornitore;
}
### End Script
### End Box
