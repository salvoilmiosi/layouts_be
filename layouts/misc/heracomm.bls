### Bill Layout Script
### Set Layout

### Box Check Numero Pagina
### Type RECTANGLE
### Mode RAW
### Page 1
### Rect 0.0048387097 0.005701254 0.9919355 0.11123422987096773
### Goto Label check_pagina
### Spacers
p+*off_allegato;
### End Spacers
### Script
$if($sys.ate) {
  $error(*ERRORE_LAYOUT_INVALIDO, "Heracomm: Stringa di Convalida Errata");
} $elifnot($search(@,/(1 \/ \d+)/)) {
  *off_allegato++;
  $goto check_pagina;
} $elifnot($contains(@,"www.gruppohera.it")) {
  *rotation=3;
}
### End Script
### End Box

### Box Check Nota Credito
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.028506271379703536 0.23387096774193547 0.620296465222349 0.12580645161290324
### Spacers
p+*off_dettaglio;
rotate+*rotation;
### End Spacers
### Script
*mul_nota_credito=$if($contains(@,"Importo a credito"),-1,1);
### End Script
### End Box

### Box Lettura Dettaglio Sinistra
### Type RECTANGLE
### Mode LAYOUT
### Page 4
### Rect 0.019455252918287938 0.14619882 0.47393317008171215 0.8187134
### Goto Label lettura_dettaglio
### Spacers
p+*off_allegato;
p+*off_dettaglio;
rotate+*rotation;
### End Spacers
### Script
$if($isempty(@) || $sys.ate) $goto nome_fornitore;
$else {
  *_dettaglio+=@;
}
### End Script
### End Box

### Box Lettura Dettaglio Destra
### Type RECTANGLE
### Mode LAYOUT
### Page 4
### Rect 0.4950413 0.14107715677419358 0.4842064302204928 0.8187134
### Spacers
p+*off_allegato;
p+*off_dettaglio;
rotate+*rotation;
### End Spacers
### Script
*_dettaglio+=@;
*off_dettaglio++;
$goto lettura_dettaglio;
### End Script
### End Box

### Box Nome Fornitore
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD
### Page 1
### Rect 0.010686489728958637 0.013723003677419354 0.15479139900142652 0.09514796406451613
### Goto Label nome_fornitore
### Script
fornitore="Heracomm";
### End Script
### End Box

### Box Dati Fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 3
### Rect 0.02892562 0.2748538 0.29008263 0.6175917825864277
### Spacers
p+*off_allegato;
rotate+*rotation;
### End Spacers
### Script
numero_fattura=$search(@,/(?:Bolletta|Fattura elettronica) n\. (\d+)/);
data_fattura=$date(@,"%d.%m.%Y",/Data emissione (\D)/);
data_scadenza=$date(@,"%d.%m.%Y",/Scadenza (\D)/);
'ragione_sociale=$search(@,/Intestata a (.+)/);
numero_cliente=$search(@,/Codice cliente (\d+)/);
'indirizzo_fornitura=$singleline($search(@,/Servizio fornito in ([^]+?)\.+/));
%potenza[0:3]=$search(@,/Potenza disponibile: (\N) kW/);
codice_pod=$search(@,/POD \(Punto di prelievo\): (\w+)/);
### End Script
### End Box

### Box Totale Fattura
### Type RECTANGLE
### Mode LAYOUT
### Page 3
### Rect 0.346281 0.12982456 0.6305785 0.70058477
### Spacers
p+*off_allegato;
rotate+*rotation;
### End Spacers
### Script
%totale_fattura=$search(@,/Totale bolletta\/contratto (\N)/);
### End Script
### End Box

### Box Parsing Dettaglio
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD
### Page 4
### Rect 0.34734598900075014 0.06527792929611916 0.028054298642533948 0.03457106274007682
### Script
$with(*_dettaglio) {
  $newview {
    $setbegin($min($indexofend(@,"Quadro di dettaglio"),$indexofend(@,"Elementi di dettaglio")));
    $setend($indexof(@,"\n"));
    mese_inizio=$month(@,"%d.%m.%Y",/dal (\D) al \D/);
    mese_fine=$month(@,"%d.%m.%Y",/dal \D al (\D)/);
    $if(mese_fine < mese_inizio) {
      $error(*ERRORE_PERIODO_INVALIDO, "Heracomm: Periodo Non Valido");
    }
    mese_fattura=$month_add(mese_inizio,*calc_mese);
  }
  _periodo=$format(/dal \d\d\.\d\d\.\d\d\d\d al \d\d\.$0/,$date_format(mese_fattura,"%m.%Y"));
  _lettura_fine=$date_format($last_day(mese_fattura),"%d\\.%m\\.%Y");
  _lettura_inizio=$format(/(?:$0|$1)/,
    $date_format($last_day($month_add(mese_fattura,-1)),"%d\\.%m\\.%Y"),
    $date_format(mese_fattura,"01\\.%m\\.%Y"));
  $between("energia attiva (kWh)","consumo rilevato energia") {
    $step($captures(@,$format(/$0 (\N) (\N) (\N) Lettura Rilevata/,_lettura_fine))) {
      .%energia_attiva_rilevata[0]; .%energia_attiva_rilevata[1]; .%energia_attiva_rilevata[2];
    }
    $step($captures(@,$format(/$0 (\N) (\N) (\N) Lettura Rilevata/,_lettura_inizio))) {
      %energia_attiva_rilevata[0]-=@; %energia_attiva_rilevata[1]-=@; %energia_attiva_rilevata[2]-=@;
    }
  }
  $between("energia reattiva (kVARh)","consumo rilevato energia") {
    $step($captures(@,$format(/$0 (\N) (\N) (\N) Lettura Rilevata/,_lettura_fine))) {
      .%energia_reattiva[0]; .%energia_reattiva[1]; .%energia_reattiva[2];
    }
    $step($captures(@,$format(/$0 (\N) (\N) (\N) Lettura Rilevata/,_lettura_inizio))) {
      %energia_reattiva[0]-=@; %energia_reattiva[1]-=@; %energia_reattiva[2]-=@;
    }
  }
  $between("potenza (kW)","..........."){
    $step($captures(@,$format(/$0 (\N) (\N) (\N) Lettura Rilevata/,_lettura_fine))) {
      .%potenza[0]; .%potenza[1]; .%potenza[2];
    }
  }
  $between("Totale servizi di Vendita","...........") {
    $newview {
      $setbegin($min($indexofend(@,"Prezzo Netto"),$indexofend(@,"Prezzo dell'energia")));
      $setend($indexof(@,"Perdite di Rete"));
      $with($search(@,$format(/Importo $0\n((?:\s+.+\n)+)/,_periodo))) {
        $step($captures(@,/fascia F1 \u20ac\/kWh (\N) (\N) kWh/)) {
          .%prezzo_energia[0]; .%energia_attiva[0];
        }
        $step($captures(@,/fascia F2 \u20ac\/kWh (\N) (\N) kWh/)) {
          .%prezzo_energia[1]; .%energia_attiva[1];
        }
        $step($captures(@,/fascia F3 \u20ac\/kWh (\N) (\N) kWh/)) {
          .%prezzo_energia[2]; .%energia_attiva[2];
        }
      }
    }
    _totale_consumi=$sum(energia_attiva[0],energia_attiva[1],energia_attiva[2]);
    $between("Dispacciamento") {
      $with($search(@,$format(/Importo $0\n((?:\s+.+\n)+)/,_periodo))) {
        dispacciamento += $num($search(@,/fascia F1 \u20ac\/kWh (\N)/)) * energia_attiva[0];
        dispacciamento += $num($search(@,/fascia F2 \u20ac\/kWh (\N)/)) * energia_attiva[1];
        dispacciamento += $num($search(@,/fascia F3 \u20ac\/kWh (\N)/)) * energia_attiva[2];
        dispacciamento = dispacciamento / _totale_consumi;
      }
    }
    $between("Sbilanciamento") {
      $foreach($search_all(@,/Sbilanciamento ((?:Importo.+\n)+)/)) {
        $if($date_between(mese_fattura,$date(@,"%d.%m.%Y",/dal (\D) al \D/),$date(@,"%d.%m.%Y",/dal \D al (\D)/))) {
          %sbilanciamento=$search(@,/\u20ac\/kWh (\N)/);
          $break;
        }
      }
    }
    $with($search_all(@,$format(/Importo $0\n((?:\s+.+\n)+)/,_periodo))) {
      ^spesa_materia_energia+=$search_all(@,/.+ (\N)/);
    }
    $foreach($search_all(@,/Importo (dal \d\d\.\d\d\.\d\d\d\d al \d\d\.\d\d\.\d\d\d\d \u20ac.+)/)) {
      $if($date_between(mese_fattura,$date(@,"%d.%m.%Y",/dal (\D) al \D/),$date(@,"%d.%m.%Y",/dal \D al (\D)/))) {
        spesa_materia_energia+=$trunc($num($search(@,/\u20ac\/kWh (\N)/))*_totale_consumi,2);
        spesa_materia_energia+=$trunc($num($search(@,/\u20ac\/cliente\/mese (\N)/)),2);
      }
    }
  }
  $between("Totale servizi di Rete","...........") {
    $between("QUOTA FISSA", "QUOTA POTENZA") {
      $foreach($search_all(@,/Importo (dal \d\d\.\d\d\.\d\d\d\d al \d\d\.\d\d\.\d\d\d\d .+)/)) {
        $if($date_between(mese_fattura,$date(@,"%d.%m.%Y",/dal (\D) al \D/),$date(@,"%d.%m.%Y",/dal \D al (\D)/))) {
          trasporto_gestione+=$trunc($abs($num($search(@,/\u20ac\/cliente\/mese (\N)/)))**mul_nota_credito, 2);
          $break;
        }
      }
    }
    $between("QUOTA POTENZA","QUOTA VARIABILE") {
      %trasporto_gestione+=$search(@,$format(/Importo $0 \u20ac\/kW di potenza \N \N kW (\N)/,_periodo));
    }
    $between("QUOTA VARIABILE") {
      $foreach($search_all(@,/Importo (dal \d\d\.\d\d\.\d\d\d\d al \d\d\.\d\d\.\d\d\d\d\n.+)/)) {
        $if($date_between(mese_fattura,$date(@,"%d.%m.%Y",/dal (\D) al \D/),$date(@,"%d.%m.%Y",/dal \D al (\D)/))) {
          trasporto_gestione+=$trunc($num($search(@,/\u20ac\/kWh (\N)/)) * _totale_consumi, 2);
          %prezzo_inf75=$search(@,/Corrispettivi energia reattiva entro 75% \u20ac\/kVARh (\N)/);
          $if(prezzo_inf75) {
            %penale_reattiva_inf75=$trunc($sum(
              $max(0, energia_reattiva[0] - energia_attiva[0]*0.33),
              $max(0, energia_reattiva[1] - energia_attiva[1]*0.33)) * prezzo_inf75, 2);
            trasporto_gestione+=penale_reattiva_inf75;
          }
          %prezzo_sup75=$search(@,/Corrispettivi energia reattiva oltre 75% \u20ac\/kVARh (\N)/);
          $if(prezzo_sup75) {
            penale_reattiva_sup75=$trunc($sum(
              $max(0, energia_reattiva[0] - energia_attiva[0]*0.75),
              $max(0, energia_reattiva[1] - energia_attiva[1]*0.75)) * prezzo_sup75, 2);
            trasporto_gestione+=penale_reattiva_sup75;
          }
        }
      }
    }
  }
  $between("Totale Imposte") {
    accise=$trunc(0.0125 * _totale_consumi * *mul_nota_credito, 2);
    iva=$search(@,/IVA (\d+%)/);
    imponibile=$sum(spesa_materia_energia,trasporto_gestione,accise);
  }
}
### End Script
### End Box

### Box Prossima Pagina
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD
### Page 4
### Rect 0.3773412702120966 0.06426464259653104 0.027093787866044883 0.03737960254547193
### Script
$if(mese_fattura != mese_fine) {
  *calc_mese++;
  $nexttable;
  $goto nome_fornitore;
}
### End Script
### End Box
