### Bill Layout Script
### Set Layout
### Language it_IT

### Box Check Numero Pagina
### Mode RAW
### Page 1
### Rect 0.0048387097 0.005701254 0.9919355 0.11123422987096773
### Goto Label check_pagina
### Spacers
p+$off_allegato;
### End Spacers
### Script
if (ate()) {
  error("Heracomm: Stringa di Convalida Errata", global ERRORE_LAYOUT_INVALIDO);
} else if (!search(@,/(1 \/ \d+)/)) {
  $off_allegato++;
  goto check_pagina;
} else if (!contains(@,"www.gruppohera.it")) {
  $rotation=3;
}
### End Script
### End Box

### Box Check Nota Credito
### Page 1
### Rect 0.028506271379703536 0.23387096774193547 0.620296465222349 0.12580645161290324
### Spacers
p+$off_dettaglio;
rotate+$rotation;
### End Spacers
### Script
$mul_nota_credito=conditional(contains(@,"Importo a credito"),-1,1);
### End Script
### End Box

### Box Lettura Dettaglio Sinistra
### Mode LAYOUT
### Page 4
### Rect 0.019455252918287938 0.14619882 0.47393317008171215 0.8187134
### Goto Label lettura_dettaglio
### Spacers
p+$off_allegato;
p+$off_dettaglio;
rotate+$rotation;
### End Spacers
### Script
if(isempty(@) || ate()) goto nome_fornitore;
else {
  $dettaglio+=@;
}
### End Script
### End Box

### Box Lettura Dettaglio Destra
### Mode LAYOUT
### Page 4
### Rect 0.4950413 0.14107715677419358 0.4842064302204928 0.8187134
### Spacers
p+$off_allegato;
p+$off_dettaglio;
rotate+$rotation;
### End Spacers
### Script
$dettaglio+=@;
$off_dettaglio++;
goto lettura_dettaglio;
### End Script
### End Box

### Box Nome Fornitore
### Flags NOREAD
### Page 1
### Rect 0.010686489728958637 0.013723003677419354 0.15479139900142652 0.09514796406451613
### Goto Label nome_fornitore
### Script
fornitore="Heracomm";
### End Script
### End Box

### Box Dati Fattura
### Page 3
### Rect 0.02892562 0.2748538 0.29008263 0.6175917825864277
### Spacers
p+$off_allegato;
rotate+$rotation;
### End Spacers
### Script
numero_fattura=search(@,/(?:Bolletta|Fattura elettronica) n\. (\d+)/);
data_fattura=search_date(@,"%d.%m.%Y",/Data emissione (\D)/);
data_scadenza=search_date(@,"%d.%m.%Y",/Scadenza (\D)/);
ragione_sociale=totitle(search(@,/Intestata a (.+)/));
numero_cliente=search(@,/Codice cliente (\d+)/);
indirizzo_fornitura=totitle(singleline(search(@,/Servizio fornito in ([^]+?)\.+/)));
potenza[0:3]=search_num(@,/Potenza disponibile: (\N) kW/);
codice_pod=search(@,/POD \(Punto di prelievo\): (\w+)/);
### End Script
### End Box

### Box Totale Fattura
### Mode LAYOUT
### Page 3
### Rect 0.346281 0.12982456 0.6305785 0.70058477
### Spacers
p+$off_allegato;
rotate+$rotation;
### End Spacers
### Script
totale_fattura=search_num(@,/Totale bolletta\/contratto (\N)/);
### End Script
### End Box

### Box Parsing Dettaglio
### Flags NOREAD
### Page 4
### Rect 0.34734598900075014 0.06527792929611916 0.028054298642533948 0.03457106274007682
### Script
with($dettaglio) {
  with(between(@,conditional(contains(@,"Quadro di dettaglio"),"Quadro di dettaglio","Elementi di dettaglio"),"\n")) {
    $mese_inizio=search_month(@,"%d.%m.%Y",/dal (\D) al \D/);
    $mese_fine=search_month(@,"%d.%m.%Y",/dal \D al (\D)/);
    if($mese_fine < $mese_inizio) {
      error("Heracomm: Periodo Non Valido", global ERRORE_PERIODO_INVALIDO);
    }
    mese_fattura=month_add($mese_inizio, $calc_mese);
  }
  $periodo=format(/dal \d\d\.\d\d\.\d\d\d\d al \d\d\.$0/,date_format(mese_fattura,"%m.%Y"));
  $lettura_fine=date_format(last_day(mese_fattura),"%d\\.%m\\.%Y");
  $lettura_inizio=format(/(?:$0|$1)/,
    date_format(last_day(month_add(mese_fattura,-1)),"%d\\.%m\\.%Y"),
    date_format(mese_fattura,"01\\.%m\\.%Y"));
  with(between(@,"energia attiva (kWh)","consumo rilevato energia")) {
    step(captures_num(@,format(/$0 (\N) (\N) (\N) Lettura Rilevata/, $lettura_fine))) {
      energia_attiva_rilevata[0]=@;
      energia_attiva_rilevata[1]=@;
      energia_attiva_rilevata[2]=@;
    }
    step(captures_num(@,format(/$0 (\N) (\N) (\N) Lettura Rilevata/, $lettura_inizio))) {
      energia_attiva_rilevata[0] -= +@;
      energia_attiva_rilevata[1] -= +@;
      energia_attiva_rilevata[2] -= +@;
    }
  }
  with(between(@,"energia reattiva (kVARh)","consumo rilevato energia")) {
    step(captures_num(@,format(/$0 (\N) (\N) (\N) Lettura Rilevata/, $lettura_fine))) {
      energia_reattiva[0]=@;
      energia_reattiva[1]=@;
      energia_reattiva[2]=@;
    }
    step(captures_num(@,format(/$0 (\N) (\N) (\N) Lettura Rilevata/, $lettura_inizio))) {
      energia_reattiva[0] -= +@;
      energia_reattiva[1] -= +@;
      energia_reattiva[2] -= +@;
    }
  }
  with(between(@,"potenza (kW)","...........")) {
    step(captures_num(@,format(/$0 (\N) (\N) (\N) Lettura Rilevata/, $lettura_fine))) {
      potenza[0]=@;
      potenza[1]=@;
      potenza[2]=@;
    }
  }
  with(between(@,"Totale servizi di Vendita","...........")) {
    with(between(@,conditional(contains(@,"Prezzo Netto"),"Prezzo Netto","Prezzo dell'energia"),"Perdite di Rete")) {
      with(search(@,format(/Importo $0\n((?:\s+.+\n)+)/, $periodo))) {
        step(captures_num(@,/fascia F1 \u20ac\/kWh (\N) (\N) kWh/)) {
          prezzo_energia[0]=@;
          energia_attiva[0]=@;
        }
        step(captures_num(@,/fascia F2 \u20ac\/kWh (\N) (\N) kWh/)) {
          prezzo_energia[1]=@;
          energia_attiva[1]=@;
        }
        step(captures_num(@,/fascia F3 \u20ac\/kWh (\N) (\N) kWh/)) {
          prezzo_energia[2]=@;
          energia_attiva[2]=@;
        }
      }
    }
    $totale_consumi=sum(energia_attiva);
    with(between(@,"Dispacciamento")) {
      with(search(@,format(/Importo $0\n((?:\s+.+\n)+)/, $periodo))) {
        dispacciamento=search_num(@,/fascia F1 \u20ac\/kWh (\N)/) * energia_attiva[0];
        dispacciamento=search_num(@,/fascia F2 \u20ac\/kWh (\N)/) * energia_attiva[1];
        dispacciamento=search_num(@,/fascia F3 \u20ac\/kWh (\N)/) * energia_attiva[2];
        dispacciamento = dispacciamento / sum(energia_attiva);
      }
    }
    with(between(@,"Sbilanciamento")) {
      foreach(matches(@,/Sbilanciamento ((?:Importo.+\n)+)/)) {
        if(date_between(mese_fattura,search_date(@,"%d.%m.%Y",/dal (\D) al \D/),search_date(@,"%d.%m.%Y",/dal \D al (\D)/))) {
          sbilanciamento=search_num(@,/\u20ac\/kWh (\N)/);
          break;
        }
      }
    }
    with(str(matches(@,format(/Importo $0\n((?:\s+.+\n)+)/, $periodo)))) {
      spesa_materia_energia+=sum(matches_num(@,/.+ (\N)/));
    }
    foreach(matches(@,/Importo (dal \d\d\.\d\d\.\d\d\d\d al \d\d\.\d\d\.\d\d\d\d \u20ac.+)/)) {
      if(date_between(mese_fattura,search_date(@,"%d.%m.%Y",/dal (\D) al \D/),search_date(@,"%d.%m.%Y",/dal \D al (\D)/))) {
        spesa_materia_energia+=trunc(search_num(@,/\u20ac\/kWh (\N)/) * $totale_consumi, 2);
        spesa_materia_energia+=trunc(search_num(@,/\u20ac\/cliente\/mese (\N)/), 2);
      }
    }
  }
  with(between(@,"Totale servizi di Rete","...........")) {
    with(between(@,"QUOTA FISSA", "QUOTA POTENZA")) {
      foreach(matches(@,/Importo (dal \d\d\.\d\d\.\d\d\d\d al \d\d\.\d\d\.\d\d\d\d .+)/)) {
        if(date_between(mese_fattura,search_date(@,"%d.%m.%Y",/dal (\D) al \D/),search_date(@,"%d.%m.%Y",/dal \D al (\D)/))) {
          trasporto_gestione+=trunc(abs(search_num(@,/\u20ac\/cliente\/mese (\N)/)) * $mul_nota_credito, 2);
          break;
        }
      }
    }
    with(between(@,"QUOTA POTENZA","QUOTA VARIABILE")) {
      trasporto_gestione+=search_num(@,format(/Importo $0 \u20ac\/kW di potenza \N \N kW (\N)/, $periodo));
    }
    with(between(@,"QUOTA VARIABILE")) {
      function calc_penale_reattiva($percent_min, $percent_max, $prezzo_energia) {
        for($i=0; $i<2; $i++) {
          $ret+=trunc(clamp(energia_reattiva[$i] - energia_attiva[$i] * $percent_min, 0, energia_attiva[$i] * ($percent_max - $percent_min))
            * $prezzo_energia,2);
        }
        return $ret;
      }
      foreach(matches(@,/Importo (dal \d\d\.\d\d\.\d\d\d\d al \d\d\.\d\d\.\d\d\d\d\n.+)/)) {
        if(date_between(mese_fattura,search_date(@,"%d.%m.%Y",/dal (\D) al \D/),search_date(@,"%d.%m.%Y",/dal \D al (\D)/))) {
          trasporto_gestione+=trunc(search_num(@,/\u20ac\/kWh (\N)/) * $totale_consumi, 2);
          prezzo_inf75=search_num(@,/Corrispettivi energia reattiva entro 75% \u20ac\/kVARh (\N)/);
          if(prezzo_inf75) {
            penale_reattiva_inf75=calc_penale_reattiva(0.33, 0.75, prezzo_inf75);
            trasporto_gestione+=penale_reattiva_inf75;
          }
          prezzo_sup75=search_num(@,/Corrispettivi energia reattiva oltre 75% \u20ac\/kVARh (\N)/);
          if(prezzo_sup75) {
            penale_reattiva_sup75=calc_penale_reattiva(0.75, 1, prezzo_sup75);
            trasporto_gestione+=penale_reattiva_sup75;
          }
        }
      }
    }
  }
  with(between(@,"Totale Imposte")) {
    accise=trunc(0.0125 * $totale_consumi * $mul_nota_credito, 2);
    iva=search(@,/IVA (\d+%)/);
    imponibile = spesa_materia_energia + trasporto_gestione + accise;
  }
}
### End Script
### End Box

### Box Prossima Pagina
### Flags NOREAD
### Page 4
### Rect 0.3773412702120966 0.06426464259653104 0.027093787866044883 0.03737960254547193
### Script
if(mese_fattura != $mese_fine) {
  $calc_mese++;
  nexttable();
  goto nome_fornitore;
}
### End Script
### End Box
