### Bill Layout Script
### Set Layout
### Language it_IT

### Box Convalida Layout
### Page 1
### Rect 0.11730205 0.16585466 0.2580645 0.04457344
### Goto Label convalida
### Script
if(!contains(@,"www.energrid.it")) {
  error("EnerGrid: Stringa di Convalida Errata", global ERRORE_LAYOUT_INVALIDO);
}
### End Script
### End Box

### Box Nome Fornitore
### Flags NOREAD
### Page 1
### Rect 0.05283649 0.015727598 0.12235818 0.09436559
### Goto Label nome_fornitore
### Script
fornitore="EnerGrid";
### End Script
### End Box

### Box Dati Cliente
### Page 1
### Rect 0.6539589 0.023841608 0.32111436 0.114025086
### Script
numero_cliente=search(@,/CODICE CLIENTE: (.+)/);
ragione_sociale=totitle(singleline(search(@,/Intestatario contratto: ([^]+?)\nCod\. Fiscale/)));
### End Script
### End Box

### Box Numero Fattura
### Page 1
### Rect 0.13343109 0.30683112 0.6979472 0.04353685
### Script
numero_fattura=search(@,/Fattura Energia n\u00b0 ([0-9]+)/);
data_fattura=search_date(@,"%d/%m/%Y",/del (\D)/);
### End Script
### End Box

### Box Dati Fattura
### Mode LAYOUT
### Page 1
### Rect 0.035190612 0.34902042 0.23020528 0.45257592
### Script
data_scadenza=search_date(@,"%d/%m/%Y",/Scadenza fattura: (\D)/);

mese_fattura=first_day(search_date(@,"%d/%m/%Y",/\D - (\D)/));
if(!mese_fattura) {
  error("EnerGrid: Mese Fattura Mancante", global ERRORE_PERIODO_INVALIDO);
}
$mese=date_format(mese_fattura,"%m/%Y");
### End Script
### End Box

### Box Riepologo Importi
### Mode LAYOUT
### Page 1
### Rect 0.2672123 0.350057 0.69011915 0.45153934
### Script
totale_fattura=search_num(@,/TOTALE DELLA FATTURA (\N)/);
codice_pod=search(@,/POD: (\w+)/);
indirizzo_fornitura=totitle(search(@,/Indirizzo di fornitura: ([^]+?\d{5}.+)/));
### End Script
### End Box

### Box Dati Fornitura
### Mode RAW
### Page 2
### Rect 0.045739044 0.10376283 0.91132927 0.21464685
### Spacers
p+$off_pod;
### End Spacers
### Script
indirizzo_fornitura=totitle(singleline(search(@,/Indirizzo di fornitura: ([^]+?\d{5}.+)\n/)));
codice_pod=search(@,/POD(?: \(punto di prelievo\)|:) (\w+)/);
potenza[0:3]=search_num(@,/Potenza contrattuale: (\N) kW/);
### End Script
### End Box

### Box Storico Consumi / Imposte
### Mode LAYOUT
### Flags PAGE
### Page 2
### Rect 0.043880455 0.5927153 0.9120019 0.23200065
### Spacers
p+$off_pod;
### End Spacers
### Script
with(between(@,"RIEPILOGO CONSUMI PRIMO ADDEBITO","TOTALE (Salvo conguagli, errori e omissioni)")) {
  step(captures_num(@,format(/\d\d\/$0 Consumi Fatturati (\N) (\N) (\N) \N (\N) (\N) (\N)/, $mese))) {
    energia_attiva_rilevata[0]=@;
    energia_attiva_rilevata[1]=@;
    energia_attiva_rilevata[2]=@;
    energia_reattiva[0]=@;
    energia_reattiva[1]=@;
    energia_reattiva[2]=@;
  }
}
with(between(@,"DETTAGLIO IMPOSTE","TOTALE (Salvo conguagli, errori e omissioni)")) {
  accise=search_num(@,format(/Imposta erariale.+ al \d\d\/$0 Euro\/kWh \N \N (\N)/, $mese));
  iva=percent(search(@,/(\N) %/));
}
### End Script
### End Box

### Box Cerca Dettaglio
### Mode LAYOUT
### Page 3
### Rect 0.03548387 0.02622577 0.92741936 0.09920182
### Goto Label cerca_dettaglio
### Spacers
p+$off_dettaglio;
### End Spacers
### Script
if(ate()) error("EnerGrid: Dettaglio Mancante", global ERRORE_DETTAGLIO_MANCANTE);
$pod_dettaglio=search(@,/POD: (.+)/);
if($pod_dettaglio!=codice_pod) {
  $off_dettaglio++;
  goto cerca_dettaglio;
}
### End Script
### End Box

### Box Dettaglio Importi
### Mode LAYOUT
### Flags PAGE
### Page 3
### Rect 0.34193546 0.12884834 0.0483871 0.031927034
### Spacers
p+$off_dettaglio;
### End Spacers
### Script
with(between(@,"DETTAGLIO IMPORTI","TOTALE (Salvo conguagli, errori e omissioni)")) {
  with(between(@,"Spese per la materia energia","Spese per trasporto")) {
    spesa_materia_energia=search_num(@,/(\N)/);
    step(captures_num(@,format(/Componente di Generazione fascia F1 .+ al \d\d\/$0 Euro\/kWh (\N) (\N)/, $mese))) {
      prezzo_energia[0]=@;
      energia_attiva[0]=@;
    }
    step(captures_num(@,format(/Componente di Generazione fascia F2 .+ al \d\d\/$0 Euro\/kWh (\N) (\N)/, $mese))) {
      prezzo_energia[1]=@;
      energia_attiva[1]=@;
    }
    step(captures_num(@,format(/Componente di Generazione fascia F3 .+ al \d\d\/$0 Euro\/kWh (\N) (\N)/, $mese))) {
      prezzo_energia[2]=@;
      energia_attiva[2]=@;
    }
  }
  with(between(@,"Spese per trasporto","Spese per oneri")) {
    trasporto_gestione=search_num(@,/(\N)/);
    penale_reattiva_inf75=sum(matches_num(@,format(/Energia Reattiva tra il \d\d e il 75% fascia F[1-3] .+ al \d\d\/$0 Euro\/kvarh \N \N (\N)/, $mese)));
    penale_reattiva_sup75=sum(matches_num(@,format(/Energia Reattiva oltre il 75% fascia F[1-3] .+ al \d\d\/$0 Euro\/kvarh \N \N (\N)/, $mese)));
  }
  with(between(@,"Spese per oneri")) {
    oneri=search_num(@,/(\N)/);
  }
}
### End Script
### End Box

### Box Prossima Pagina
### Page 3
### Rect 0.017741935 0.034207527 0.31774193 0.15507412
### Goto Label check_pagina
### Spacers
p+$off_pod;
### End Spacers
### Script
if (!(ate() || contains(@,"INFORMAZIONI AI CLIENTI"))) {
  $off_pod++;
  if(contains(@,"DATI FORNITURA")) {
    nexttable();
    goto nome_fornitore;
  } else {
    goto check_pagina;
  }
}
### End Script
### End Box
