### Bill Layout Script
### Flag Find Layout
### Language it_IT

### Box Nome Fornitore
### Flags NOREAD
### Page 1
### Rect 0.0580645 0.018244 0.35 0.114025
### Script
fornitore="Lenergia";
### End Script
### End Box

### Box Dati fattura
### Mode LAYOUT
### Page 1
### Rect 0.512487 0.0275867 0.474766 0.12414
### Script
numero_cliente=search(@,/Codice Cliente: (.+)\n/);
numero_fattura=search(@,/fattura n\. (\N) /);
data_fattura=search_date(@,"%d/%m/%Y",/fattura n\. \N del (\D)/);
mese_fattura=search_month(@,"%d/%m/%Y",/Periodo \D - (\D)/);
$mese=date_format(mese_fattura,"%m/%Y");
### End Script
### End Box

### Box Totale fattura
### Page 1
### Rect 0.110562 0.362306 0.332986 0.0358627
### Script
totale_fattura = +@;
### End Script
### End Box

### Box Data scadenza
### Page 1
### Rect 0.112903 0.420753 0.333871 0.0342075
### Script
data_scadenza=search_date(@,"%d/%m/%Y");
### End Script
### End Box

### Box Intestatario della fattura
### Page 1
### Rect 0.105359 0.220694 0.348595 0.0551734
### Script
ragione_sociale=totitle(search(@,/^(.+)\n/));
indirizzo_fornitura=totitle(singleline(search(@,/^.+\n([^]+?\d{5}.+)\n/)));
### End Script
### End Box

### Box Sintesi degli importi fatturati
### Mode LAYOUT
### Page 1
### Rect 0.51769 0.340236 0.469563 0.205061
### Script
spesa_materia_energia=search_num(@,/Spesa per la materia Energia (\N)\n/);
trasporto_gestione=search_num(@,/Servizi di rete (\N)\n/);
oneri=search_num(@,/Spesa per oneri di sistema (\N)\n/);
### End Script
### End Box

### Box Dettaglio fiscale/IVA
### Mode LAYOUT
### Page 1
### Rect 0.516389 0.570125 0.474766 0.0855188
### Script
with(captures(@,/IVA (\N%) (\N)/)) {
  iva=@[0];
  imponibile = +@[1];
}
### End Script
### End Box

### Box Dati Fornitura
### Mode LAYOUT
### Page 2
### Rect 0.0684262 0.0233233 0.904203 0.17188616
### Script
codice_pod=search(@,/Codice punto di erogazione \(POD\): ([a-zA-Z0-9]+)/);
### End Script
### End Box

### Box Energia Reattiva / Potenza
### Mode LAYOUT
### Page 2
### Rect 0.035119668 0.2005698 0.92351717 0.6100175
### Script
(energia_attiva_rilevata[0], energia_attiva_rilevata[1], energia_attiva_rilevata[2])
  = captures_num(@,format(/\d\d\/$0 Energia Attiva kWh (\N) (\N) (\N)/, $mese));
(energia_reattiva[0], energia_reattiva[1], energia_reattiva[2])
  = captures_num(@,format(/\d\d\/$0 Energia Reattiva kvarh (\N) (\N) (\N)/, $mese));
(potenza[0], potenza[1], potenza[2])
  = captures_num(@,format(/\d\d\/$0 Potenza kW (\N) (\N) (\N)/, $mese));
### End Script
### End Box

### Box Quadro Dettaglio
### Mode LAYOUT
### Page 5
### Rect 0.053763438 0.06647673 0.912485 0.8831909
### Goto Label quadro_dettaglio
### Spacers
p+$page_offset;
### End Spacers
### Script
(prezzo_energia[0], energia_attiva[0])
  = captures_num(@,format(/Indice mese di competenza F1 .+ \d\d\/$0 \u20ac\/kWh (\N) (\N)/, $mese));
(prezzo_energia[1], energia_attiva[1])
  = captures_num(@,format(/Indice mese di competenza F2 .+ \d\d\/$0 \u20ac\/kWh (\N) (\N)/, $mese));
(prezzo_energia[2], energia_attiva[2])
  = captures_num(@,format(/Indice mese di competenza F3 .+ \d\d\/$0 \u20ac\/kWh (\N) (\N)/, $mese));

pcv=search_num(@,format(/PCV .+ \d\d\/$0 \u20ac\/Pdp\/mese \N \N (\N)/, $mese));
accise=search_num(@,format(/Imposta erariale di consumo .+ \d\d\/$0 \u20ac\/kWh \N \N (\N)/, $mese));
penale_reattiva_inf75=search_num(@,/Penale Reattiva 33%.+kvarh \N \N (\N)/);
penale_reattiva_sup75=search_num(@,/Penale Reattiva 75%.+kvarh \N \N (\N)/);
### End Script
### End Box

### Box Controllo Prossima Pagina
### Flags PAGE SPACER 
### Page 6
### Rect 0.091051 0.038599394 0.03381893 0.023894865
### Spacers
p+$page_offset;
### End Spacers
### Script
if (!ate()) {
  $page_offset++;
  goto quadro_dettaglio;
}
### End Script
### End Box
