### Bill Layout Script
### Flag Find Layout
### Language it_IT

### Box Ricerca Dettaglio
### Page 3
### Rect 0.05483871 0.08095781 0.2629032 0.028506272
### Goto Label ricerca_dettaglio
### Flags TRIM
### Spacers
p+$off_dettaglio;
### End Spacers
### Script
if(ate()) {
  error("Estra: Dettaglio Mancante", global ERRORE_DETTAGLIO_MANCANTE);
} else if(@ != "ELEMENTI DI DETTAGLIO") {
  $off_dettaglio++;
  goto ricerca_dettaglio;
}
### End Script
### End Box

### Box Lettura Dettaglio
### Mode LAYOUT
### Page 3
### Rect 0.04032258 0.114025086 0.9370968 0.8540479
### Goto Label lettura_dettaglio
### Spacers
p+$off_dettaglio;
### End Spacers
### Script
if(!ate()) {
  $dettaglio+=replace(@,"\u2212","-");
  $off_dettaglio++;
  goto lettura_dettaglio;
}
### End Script
### End Box

### Box Nome Fornitore
### Flags NOREAD
### Page 1
### Rect 0.051612902 0.018244013 0.2516129 0.06841505
### Goto Label nome_fornitore
### Script
fornitore="Estra";
### End Script
### End Box

### Box Numero Fattura / Periodo
### Mode LAYOUT
### Page 1
### Rect 0.5483871 0.02736602 0.4080645 0.0672748
### Script
numero_fattura=search(@,/Fattura N\u00b0 (\d+)/);
data_fattura=search_date(@,"%d.%m.%Y",/del (\D)/);
$mese_inizio=search_month(@,"%d.%m.%Y",/(\D) - \D/);
$mese_fine=search_month(@,"%d.%m.%Y",/\D - (\D)/);
if($mese_fine < $mese_inizio) {
  error("Estra: Periodo Non Valido", global ERRORE_PERIODO_INVALIDO);
}
mese_fattura=month_add($mese_inizio, $calc_mese);
$periodo=format(/\d\d\.\d\d.\d\d\d\d - \d\d\.$0/,date_format(mese_fattura,"%m.%Y"));
### End Script
### End Box

### Box Intestatario
### Page 1
### Rect 0.05 0.12656784 0.46129033 0.19156215
### Script
numero_cliente=search(@,/CODICE CLIENTE (\d+)/);
ragione_sociale=totitle(singleline(search(@,/INTESTATARIO FORNITURA ([^]+?) C\.F\./)));
### End Script
### End Box

### Box Totale / Scadenza
### Page 1
### Rect 0.05 0.32155073 0.92419356 0.05359179
### Script
totale_fattura=search_num(@,/euro (\N)/);
data_scadenza=search_date(@,"%d.%m.%Y",/entro il (\D)/);
### End Script
### End Box

### Box Dati Fornitura
### Page 2
### Rect 0.045161292 0.11516534 0.45806453 0.15507412
### Script
codice_pod=search(@,/POD \(Punto di prelievo\) (\w+)/);
potenza=repeated(search_num(@,/Potenza impegnata (\N) kW/), 3);
indirizzo_fornitura=totitle(singleline(search(@,/Indirizzo di fornitura ([^]+?\d{5}.+)/)));
### End Script
### End Box

### Box Consumi Rilevati
### Mode LAYOUT
### Page 2
### Rect 0.053225808 0.31470925 0.92419356 0.64766246
### Script
energia_attiva_rilevata=captures_num(between(@,"CONSUMI FATTURATI","RIEPILOGO IMPOSTE E IVA"),
  format(/Effettivi $0 \N (\N) (\N) (\N)/, $periodo));
### End Script
### End Box

### Box Parsing Dettaglio
### Flags NOREAD
### Page 3
### Rect 0.32258067 0.079041496 0.048387095 0.028506272
### Script
with($dettaglio) {
  with(between(@,"TOTALE MATERIA ENERGIA","TOTALE TRASPORTO E GESTIONE CONTATORE")) {
    spesa_materia_energia=sum(matches_num(@,format(/$0 .+ (\N) \N/, $periodo)));
    pcv=search_num(@,format(/Commercializzazione vendita $0 \u20ac\/POD\/mese \N \N (\N)/, $periodo));
    tie(prezzo_energia[0], energia_attiva[0])
      = captures_num(@,format(/Energia F1 $0 \u20ac\/kWh (\N) (\N)/, $periodo));
    tie(prezzo_energia[1], energia_attiva[1])
      = captures_num(@,format(/Energia F2 $0 \u20ac\/kWh (\N) (\N)/, $periodo));
    tie(prezzo_energia[2], energia_attiva[2])
      = captures_num(@,format(/Energia F3 $0 \u20ac\/kWh (\N) (\N)/, $periodo));
    sbilanciamento=search_num(@,format(/Sbilanciamento $0 \u20ac\/kWh (\N)/, $periodo));
  }
  with(between(@,"TOTALE TRASPORTO E GESTIONE CONTATORE","TOTALE ONERI DI SISTEMA")) {
    trasporto_gestione=sum(matches_num(@,format(/$0 .+ (\N) \N/, $periodo)));
    potenza=repeated(search_num(@,format(/Quota potenza $0 \u20ac\/kW \N (\N)/, $periodo)), 3);
  }
  with(between(@,"TOTALE ONERI DI SISTEMA","TOTALE ALTRE IMPOSTE")) {
    oneri=sum(matches_num(@,format(/$0 .+ (\N) \N/, $periodo)));
  }
  with(between(@,"TOTALE ALTRE IMPOSTE","TOTALE FORNITURA")) {
    accise=search_num(@,format(/Imposta erariale di consumo $0 \d+\u00b0 \u20ac\/kWh \N \N (\N)/, $periodo));
  }
  with(between(@,"TOTALE ALTRE PARTITE","TOTALE FORNITURA")) {
    altre_partite=sum(matches_num(@,format(/$0 .+ (\N) \N/, $periodo)));
  }
  with(between(@,"TOTALE FORNITURA","TOTALE DA PAGARE")) {
    iva=search(@,/IVA (\d+%)/);
  }
  imponibile = spesa_materia_energia + trasporto_gestione + oneri + accise + altre_partite;
}
### End Script
### End Box

### Box Prossima pagina
### Flags NOREAD
### Page 3
### Rect 0.3844086 0.07889734 0.043010753 0.02756654
### Script
if(mese_fattura!=$mese_fine) {
  $calc_mese++;
  nexttable();
  goto nome_fornitore;
}
### End Script
### End Box
