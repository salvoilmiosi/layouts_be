### Bill Layout Script
### Set Layout

### Box Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Flags TRIM
### Page 1
### Rect 0.7381144 0.0045636054 0.23529412 0.022818027
### Script
$if(@ != "ENERGIA ELETTRICA") {
  $error(*ERRORE_LAYOUT_INVALIDO, "Edison: Stringa di Convalida Errata");
}
### End Script
### End Box

### Box Nome Fornitore
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD
### Page 1
### Rect 0.016116036 0.036508843 0.20145044 0.07529949
### Goto Label nome_fornitore
### Script
fornitore="Edison";
### End Script
### End Box

### Box Numero / Data Fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.33843675 0.02457326 0.31240317 0.018429944
### Script
numero_fattura=$search(@,/Fattura N\u00b0 (\N)/);
data_fattura=$date(@,"%d %B %Y");
### End Script
### End Box

### Box Ragione Sociale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.008058018 0.11409014 0.21112007 0.092413
### Script
'ragione_sociale=$search(@,/Spettabile\n(.+)/);
### End Script
### End Box

### Box Dati Fornitura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.33843675 0.10610382 0.29814664 0.10952653
### Script
'indirizzo_fornitura=$singleline($search(@,/Punto di fornitura\n(.+\n.+)/));
potenza[0:3]=$search(@,/Potenza impegnata: (\N) kW/);
### End Script
### End Box

### Box N Cliente / POD
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.6817083 0.100399315 0.30136988 0.057045065
### Script
numero_cliente=$search(@,/Codice Cliente (.+)/);
codice_pod=$search(@,/POD (.+)/);
### End Script
### End Box

### Box Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.33843675 0.0045636054 0.3142627 0.019395322
### Script
mese_fattura=$month(@,"%B %Y");
mese_fine=$month(@,"%B %Y",/\D - (\D)/);
$if(mese_fine) {
  $if(mese_fine < mese_fattura) {
    $error(*ERRORE_PERIODO_INVALIDO, "Edison: Periodo Non Valido");
  }
  mese_fattura=$month_add(mese_fattura,*calc_mese);
}
_mese=$date_format(mese_fattura,"%m/%Y");
### End Script
### End Box

### Box Totale / Scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.031412613 0.35290596 0.24993515 0.14338613
### Script
%totale_fattura=$search(@,/(\N)/);
data_scadenza=$date(@,"%d %B %Y");
### End Script
### End Box

### Box Consumi Rilevati
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### Rect 0.32505226 0.58302 0.6542018 0.22527969
### Script
$step($captures($replace(@,"\u2212","-"),
  $format(/\w+ \d\d\/\d\d\/\d\d\d\d - \d\d\/$0 (\N) (\N) (\N)/,_mese)))
{
  .%energia_attiva_rilevata[0]; .%energia_attiva_rilevata[1]; .%energia_attiva_rilevata[2];
}
### End Script
### End Box

### Box Dettaglio Spese
### Type RECTANGLE
### Mode LAYOUT
### Page 4
### Rect 0.013715775 0.0849123 0.96867657 0.8248623
### Script
$with($replace(@,"\u2212","-")) {
  $step($captures(@,$format(/Componente energia - F1 .+ al \d\d\/$0 \u20ac\/kWh (\N) (\N) kWh/,_mese))) {
    .%prezzo_energia[0]; .%energia_attiva[0];
  }
  $step($captures(@,$format(/Componente energia - F2 .+ al \d\d\/$0 \u20ac\/kWh (\N) (\N) kWh/,_mese))) {
    .%prezzo_energia[1]; .%energia_attiva[1];
  }
  $step($captures(@,$format(/Componente energia - F3 .+ al \d\d\/$0 \u20ac\/kWh (\N) (\N) kWh/,_mese))) {
    .%prezzo_energia[2]; .%energia_attiva[2];
  }
  %potenza[0:3]=$search(@,$format(/Quota potenza .+ al \d\d\/$0 \u20ac\/kW\/mese \N (\N) kW/,_mese));
  iva=$search(@,/IVA (\N%)/);
  $between("Spesa per l'energia","Spesa per il trasporto") {
    ^spesa_materia_energia=$search_all(@,$format(/Dal .+ al \d\d\/$0.+ (\N) \u20ac/,_mese));
  }
  $between("Spesa per il trasporto","Spesa per gli oneri") {
    ^trasporto_gestione=$search_all(@,$format(/Dal .+ al \d\d\/$0.+ (\N) \u20ac/,_mese));
  }
  $between("Spesa per gli oneri","Totale Imposte") {
    ^oneri=$search_all(@,$format(/Dal .+ al \d\d\/$0.+ (\N) \u20ac/,_mese));
  }
  $between("Totale Imposte") {
    %accise=$search(@,$format(/Imposta erariale di consumo \(accisa\) .+ al \d\d\/$0 \u20ac\/kWh \N \N kWh (\N) \u20ac/,_mese));
  }
  imponibile=$sum(spesa_materia_energia, trasporto_gestione, oneri, accise);
  $between("Imposte e IVA","TOTALE") {
    ^imponibile+=$search_all(@,$format(/Dal .+ al \d\d\/$0.+ (\N) \u20ac/,_mese));
  }
}
### End Script
### End Box

### Box Prossima Pagina
### Type RECTANGLE
### Mode DEFAULT
### Page 4
### Rect 0.2610798 0.046750285 0.0467365 0.030786773
### Script
$if(mese_fine) {
  $if(mese_fattura != mese_fine) {
    *calc_mese++;
    $nexttable;
    $goto nome_fornitore;
  }
}
### End Script
### End Box
