### Bill Layout Script
### Set Layout
### Language it_IT

### Box Convalida Layout
### Flags TRIM
### Page 1
### Rect 0.7381144 0.0045636054 0.23529412 0.022818027
### Script
if(@ != "ENERGIA ELETTRICA") {
  error("Edison: Stringa di Convalida Errata", global ERRORE_LAYOUT_INVALIDO);
}
### End Script
### End Box

### Box Nome Fornitore
### Flags NOREAD
### Page 1
### Rect 0.016116036 0.036508843 0.20145044 0.07529949
### Goto Label nome_fornitore
### Script
fornitore="Edison";
### End Script
### End Box

### Box Numero / Data Fattura
### Page 1
### Rect 0.33843675 0.02457326 0.31240317 0.018429944
### Script
numero_fattura=search(@,/Fattura N\u00b0 (\N)/);
data_fattura=search_date(@,"%d %B %Y");
### End Script
### End Box

### Box Ragione Sociale
### Page 1
### Rect 0.008058018 0.11409014 0.21112007 0.092413
### Script
ragione_sociale=totitle(search(@,/Spettabile\n(.+)/));
### End Script
### End Box

### Box Dati Fornitura
### Page 1
### Rect 0.33843675 0.10610382 0.29814664 0.10952653
### Script
indirizzo_fornitura=totitle(singleline(search(@,/Punto di fornitura\n(.+\n.+)/)));
potenza[0:3]=search(@,/Potenza impegnata: (\N) kW/);
### End Script
### End Box

### Box N Cliente / POD
### Page 1
### Rect 0.6817083 0.100399315 0.30136988 0.057045065
### Script
numero_cliente=search(@,/Codice Cliente (.+)/);
codice_pod=search(@,/POD (.+)/);
### End Script
### End Box

### Box Periodo
### Page 1
### Rect 0.33843675 0.0045636054 0.3142627 0.019395322
### Script
mese_fattura=search_date(@,"%B %Y");
mese_fine=search_date(@,"%B %Y",/\D - (\D)/);
if(mese_fine) {
  if(mese_fine < mese_fattura) {
    error("Edison: Periodo Non Valido", global ERRORE_PERIODO_INVALIDO);
  }
  mese_fattura=month_add(mese_fattura, $calc_mese);
}
$mese=date_format(mese_fattura,"%m/%Y");
### End Script
### End Box

### Box Totale / Scadenza
### Page 1
### Rect 0.031412613 0.35290596 0.24993515 0.14338613
### Script
totale_fattura=search_num(@,/(\N)/);
data_scadenza=search_date(@,"%d %B %Y");
### End Script
### End Box

### Box Consumi Rilevati
### Mode LAYOUT
### Page 1
### Rect 0.32505226 0.58302 0.6542018 0.22527969
### Script
step(captures_num(replace(@,"\u2212","-"),
  format(/\w+ \d\d\/\d\d\/\d\d\d\d - \d\d\/$0 (\N) (\N) (\N)/, $mese)))
{
  energia_attiva_rilevata[0]=@;
  energia_attiva_rilevata[1]=@;
  energia_attiva_rilevata[2]=@;
}
### End Script
### End Box

### Box Dettaglio Spese
### Mode LAYOUT
### Page 4
### Rect 0.013715775 0.0849123 0.96867657 0.8248623
### Script
with(replace(@,"\u2212","-")) {
  step(captures_num(@,format(/Componente energia - F1 .+ al \d\d\/$0 \u20ac\/kWh (\N) (\N) kWh/, $mese))) {
    prezzo_energia[0]=@;
    energia_attiva[0]=@;
  }
  step(captures_num(@,format(/Componente energia - F2 .+ al \d\d\/$0 \u20ac\/kWh (\N) (\N) kWh/, $mese))) {
    prezzo_energia[1]=@;
    energia_attiva[1]=@;
  }
  step(captures_num(@,format(/Componente energia - F3 .+ al \d\d\/$0 \u20ac\/kWh (\N) (\N) kWh/, $mese))) {
    prezzo_energia[2]=@;
    energia_attiva[2]=@;
  }
  potenza[0:3]=search_num(@,format(/Quota potenza .+ al \d\d\/$0 \u20ac\/kW\/mese \N (\N) kW/, $mese));
  iva=search(@,/IVA (\N%)/);
  with(between(@,"Spesa per l'energia","Spesa per il trasporto")) {
    spesa_materia_energia=sum(matches_num(@,format(/Dal .+ al \d\d\/$0.+ (\N) \u20ac/, $mese)));
  }
  with(between(@,"Spesa per il trasporto","Spesa per gli oneri")) {
    trasporto_gestione=sum(matches_num(@,format(/Dal .+ al \d\d\/$0.+ (\N) \u20ac/, $mese)));
  }
  with(between(@,"Spesa per gli oneri","Totale Imposte")) {
    oneri=sum(matches_num(@,format(/Dal .+ al \d\d\/$0.+ (\N) \u20ac/, $mese)));
  }
  with(between(@,"Totale Imposte")) {
    accise=search_num(@,format(/Imposta erariale di consumo \(accisa\) .+ al \d\d\/$0 \u20ac\/kWh \N \N kWh (\N) \u20ac/, $mese));
  }
  imponibile = spesa_materia_energia + trasporto_gestione + oneri + accise;
  with(between(@,"Imposte e IVA","TOTALE")) {
    imponibile+=sum(matches_num(@,format(/Dal .+ al \d\d\/$0.+ (\N) \u20ac/, $mese)));
  }
}
### End Script
### End Box

### Box Prossima Pagina
### Page 4
### Rect 0.2610798 0.046750285 0.0467365 0.030786773
### Script
if(mese_fine) {
  if(mese_fattura != mese_fine) {
    $calc_mese++;
    nexttable();
    goto nome_fornitore;
  }
}
### End Script
### End Box
