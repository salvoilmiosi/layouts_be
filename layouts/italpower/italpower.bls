### Bill Layout Script
### Set Layout
### Language it_IT

### Box Cerca Dettaglio
### Flags TRIM 
### Page 4
### Rect 0.04516129032258064 0.08893956670467502 0.2596774193548387 0.037628278221208664
### Goto Label cerca_dettaglio
### Spacers
p+$off_dettaglio;
p+$next_page;
### End Spacers
### Script
if(ate()) {
  error("Italpower: Dettaglio Mancante", global ERRORE_DETTAGLIO_MANCANTE);
} else if(@ == "ELEMENTI DI DETTAGLIO") {
  goto lettura_dettaglio;
} else {
  $off_dettaglio++;
  goto cerca_dettaglio;
}
### End Script
### End Box

### Box Lettura Dettaglio
### Mode RAW
### Page 4
### Rect 0.024193548387096774 0.08323831242873432 0.9258064516129032 0.8289623717217788
### Goto Label lettura_dettaglio
### Spacers
p+$off_dettaglio;
p+$next_page;
### End Spacers
### Script
if(ate()) {
  error("Italpower: Dettaglio Mancante", global ERRORE_DETTAGLIO_MANCANTE);
} else if(!(contains(@,"AGGIORNAMENTI CORRISPETTIVI") || contains(@,"DATI FORNITURA"))) {
  $dettaglio+=@;
  $off_dettaglio++;
  goto lettura_dettaglio;
} else {
  goto nome_fornitore;
}
### End Script
### End Box

### Box Nome Fornitore
### Flags NOREAD 
### Page 1
### Rect 0.038709678 0.050171036 0.33225808 0.07639681
### Goto Label nome_fornitore
### Script
fornitore="Italpower";
### End Script
### End Box

### Box Dati Cliente
### Mode LAYOUT
### Page 1
### Rect 0.045161292 0.16647662 0.42580646 0.13568985
### Script
numero_cliente=search(@,/CODICE CLIENTE: (\w+)/);
ragione_sociale=totitle(search(@,/Intestatario del contratto: (.+)\n/));
### End Script
### End Box

### Box Numero / Data Fattura
### Page 1
### Rect 0.043548387 0.30216646 0.43387097 0.076950006
### Script
numero_fattura=search(@,/N\u00b0 (\w+)/);
data_fattura=search_date(@,"%d/%m/%Y");
### End Script
### End Box

### Box Periodo
### Page 1
### Rect 0.048863639999999986 0.37829003371900827 0.4227273 0.05476781752066118
### Script
function parse_periodo($str_periodo) {
  if(ismatch($str_periodo,/\d+\s*-\s*\d+ \w+\. \d+/)) { # es. 01-31 gen 2021
    return list(
      search_date($str_periodo, "%b. %Y"),
      search_date($str_periodo, "%b. %Y"));
  } else if(ismatch($str_periodo,/\d+ \w+\.\s*-\s*\d+ \w+\. \d+/)) { # es. 01 gen.-31 mar. 2021
    $items=captures($str_periodo,/\d+ (\w+)\.\s*-\s*\d+ (\w+)\. (\d+)/);
    return list(
      date(format("$0 $1", $items[0], $items[2]), "%b %Y"),
      date(format("$0 $1", $items[1], $items[2]), "%b %Y"));
  } else if(ismatch($str_periodo,/\d+ \w+\. \d+\s*-\s*\d+ \w+ \d+/)) { # es. 01 dic. 2020-31 gen. 2021
    return list(
      first_day(search_date($str_periodo,"%d %b. %Y",/(\D)\s*-\s*\D/)),
      first_day(search_date($str_periodo,"%d %b. %Y",/\D\s*-\s*(\D)/)));
  } else if($str_periodo) {
    error("Italpower: Formato Periodo Non Valido", global ERRORE_PERIODO_INVALIDO);
  }
}

step(parse_periodo(search(@,/Periodo fatturato (.+)/))) {
  $mese_inizio=@;
  $mese_fine=@;
}

step(parse_periodo(search(@,/Periodo conguagliato (.+)/))) {
  $mese_inizio_conguaglio=@;
  $mese_fine_conguaglio=@;
}

mese_fattura=month_add(conditional($calc_conguaglio, $mese_inizio_conguaglio, $mese_inizio), $calc_mese);
### End Script
### End Box

### Box Totale Fattura / Scadenza
### Mode LAYOUT
### Page 1
### Rect 0.5302594 0.44806516 0.4034582 0.10997964
### Script
totale_fattura=search_num(@,/IMPORTO DA PAGARE \u20ac (\N)/);
data_scadenza=search_date(@,"%d %B %Y");
### End Script
### End Box

### Box Indirizzo Fornitura
### Mode LAYOUT
### Page 3
### Rect 0.044354837 0.1378327 0.43145162 0.23479088
### Spacers
p+$next_page;
### End Spacers
### Script
indirizzo_fornitura=totitle(singleline(search(@,/Le stiamo fornendo energia elettrica in \u2022 ([^]+?\d{5}.+)\n/)));
### End Script
### End Box

### Box POD / Potenza Impegnata
### Mode LAYOUT
### Page 3
### Rect 0.5039354178097982 0.08221476510067115 0.4408602 0.2961122348993288
### Spacers
p+$next_page;
### End Spacers
### Script
codice_pod=search(@,/NUMERO POD: (\w+)/);
potenza[0:3]=search_num(@,/Potenza impegnata: (\N) kW/);
### End Script
### End Box

### Box Energia Attiva Rilevata / Reattiva
### Mode LAYOUT
### Flags PAGE 
### Page 3
### Rect 0.048387095 0.6624857 0.89870286 0.055942718
### Spacers
p+$next_page;
### End Spacers
### Script
with(between(@,"RIEPILOGO LETTURE E CONSUMI","CONSUMI FATTURATI")) {
  $header=table_header(@,/Dal Al/,/Consumi/,/Consumi/);
  foreach(captures(@,format(/(.+\d\d\/$0 F1.+)\n(.+F2.+)\n(.+F3.+)/,date_format(mese_fattura,"%m/%Y")))) {
    step(nums(table_row(@, $header))) {
      ;
      energia_attiva_rilevata[]=@;
      energia_reattiva[]=@;
    }
  }
}
### End Script
### End Box

### Box Parsing Dettaglio
### Flags NOREAD 
### Page 4
### Rect 0.22715053909677418 0.027588217240592926 0.04543010606451612 0.03170482722919042
### Script
function regex_periodo() {
  return format(/\d\d\/\d\d\/\d\d\d\d-\s*\d\d\/$0/,date_format(mese_fattura,"%m/%Y"));
}
function regex_periodo_generic() {
  return /\d{2}\/\d{2}\/\d{4}\s*-\s*\d{2}\/\d{2}\/\d{4}/;
}
function check_periodo(@) {
  return date_between(mese_fattura,
    search_date(@,"%d/%m/%Y",/(\D)\s*-\s*\D/),
    search_date(@,"%d/%m/%Y",/\D\s*-\s*(\D)/));
}
function calc_totale_parziale(@) {
  foreach(matches(@,format(/Periodo ($0 Euro\/POD\/mese \N)/, regex_periodo_generic()))) {
    if(check_periodo()) {
      $ret+=trunc(search_num(@,/Euro\/POD\/mese (\N)/),2);
    }
  }
  $ret+=sum(matches_num(@,format(/Periodo $0 Euro\/kW\/mese \N \N (\N)/, regex_periodo())));
  foreach(matches(@,format(/Periodo ($0 Euro\/kWh \N)/, regex_periodo_generic()))) {
    if(check_periodo()) {
      $ret+=trunc(search_num(@,/Euro\/kWh (\N)/) * sum(energia_attiva),2);
    }
  }
  return $ret;
}
function calc_penale_reattiva(@, $percent_min, $percent_max, $format_str) {
  for($i=0; $i<3; $i++) {
    foreach(matches(@,format(/Energia Reattiva \($2\) - F$1 - Periodo ($0 Euro\/kVarh \N)/, regex_periodo_generic(), $i+1, $format_str))) {
      if(check_periodo()) {
        $ret+=trunc(clamp(energia_reattiva[$i] - energia_attiva[$i] * $percent_min, 0, energia_attiva[$i] * ($percent_max - $percent_min))
          * search_num(@,/Euro\/kVarh (\N)/),2);
      }
    }
  }
  return $ret;
}
with($dettaglio) {
  with(between(@,"","Totale materia energia")) {
    foreach(matches(@,/Acconti fatture precedenti per servizi di vendita su kWh \N - Periodo (.+ \N)/)) {
      if(check_periodo()) {
        spesa_materia_energia+=search_num(@,/(\N)$/);
      }
    }
    foreach(matches(@,format(/Commercializzazione e vendita \(PCV1\) - Periodo ($0 Euro\/POD\/mese \N)/, regex_periodo_generic()))) {
      if(check_periodo()) {
        pcv=trunc(search_num(@,/Euro\/POD\/mese (\N)/),2);
      }
    }
    foreach(matches(@,format(/Periodo ($0 Euro\/POD\/mese \N)/, regex_periodo_generic()))) {
      if(check_periodo()) {
        spesa_materia_energia+=trunc(search_num(@,/Euro\/POD\/mese (\N)/),2);
      }
    }
    step(captures_num(@,format(/Energia - F1 - Periodo $0 Euro\/kWh (\N) (\N) (\N)/, regex_periodo()))) {
      prezzo_energia[0]=@;
      energia_attiva[0]=@;
      spesa_materia_energia+=trunc(@,2);
    }
    step(captures_num(@,format(/Energia - F2 - Periodo $0 Euro\/kWh (\N) (\N) (\N)/, regex_periodo()))) {
      prezzo_energia[1]=@;
      energia_attiva[1]=@;
      spesa_materia_energia+=trunc(@,2);
    }
    step(captures_num(@,format(/Energia - F3 - Periodo $0 Euro\/kWh (\N) (\N) (\N)/, regex_periodo()))) {
      prezzo_energia[2]=@;
      energia_attiva[2]=@;
      spesa_materia_energia+=trunc(@,2);
    }
    foreach(matches(@,format(/Perdite di rete - F[1-3] - Periodo $0 Euro\/kWh \N (\N \N)/, regex_periodo()))) {
      step(captures_num(@,/(\N) (\N)/)) {
        perdite_di_rete+=@;
        spesa_materia_energia+=@;
      }
    }
    foreach(matches(@,format(/Dispacciamento - Periodo ($0 Euro\/kWh \N)/, regex_periodo_generic()))) {
      if(check_periodo()) {
        disp_var=search_num(@,/Euro\/kWh (\N)/);
        spesa_materia_energia+=trunc(disp_var * (sum(energia_attiva) + perdite_di_rete),2);
      }
    }
  }
  with(between(@,"Totale materia energia","Totale Trasporto e gestione del contatore")) {
    foreach(matches(@,/Acconti fatture precedenti per oneri di trasporto e gestione del contatore su kWh \N - Periodo (.+ \N)/)) {
      if(check_periodo()) {
        trasporto_gestione+=search_num(@,/(\N)$/);
      }
    }
    trasporto_gestione+=calc_totale_parziale();
    potenza[0:3]=search_num(@,format(/Quota Potenza - trasporto - Periodo $0 Euro\/kW\/mese \N (\N)/, regex_periodo()));

    penale_reattiva_inf75=calc_penale_reattiva(0.33, 0.75, "Tra il 33 % ed il 75 %");
    penale_reattiva_sup75=calc_penale_reattiva(0.75, 1, "Oltre il 75 %");

    trasporto_gestione+=penale_reattiva_inf75 + penale_reattiva_sup75;
  }
  with(between(@,"Totale Trasporto e gestione del contatore","Totale oneri di sistema")) {
    foreach(matches(@,/Acconti fatture precedenti per oneri di sistema su kWh \N - Periodo (.+ \N)/)) {
      if(check_periodo()) {
        oneri+=search_num(@,/(\N)$/);
      }
    }
    oneri+=calc_totale_parziale();
  }
  with(between(@,"Totale oneri di sistema","Totale imposte")) {
    foreach(matches(@,/Acconti fatture precedenti per Imposte su kWh \N - Periodo (.+ \N)/)) {
      if(check_periodo()) {
        accise+=search_num(@,/(\N)$/);
      }
    }
    if(contains(@,"Accisa sull'energia elettrica")) {
      accise+=trunc(sum(energia_attiva) * 0.0125,2);
    }
  }
  imponibile = spesa_materia_energia + trasporto_gestione + oneri + accise;
  iva=search(@,/IVA (\d+%)/);
}
### End Script
### End Box

### Box Prossima Pagina
### Flags TRIM 
### Page 4
### Rect 0.05161290322580646 0.14481185860889398 0.18870967741935485 0.030786773090079815
### Spacers
p+$next_page;
p+$off_dettaglio;
### End Spacers
### Script
if(!$calc_conguaglio) {
  if(mese_fattura != $mese_fine) {
    $calc_mese++;
    nexttable();
    goto nome_fornitore;
  } else if($mese_inizio_conguaglio) {
    $calc_conguaglio = 1;
    $calc_mese = 0;
    nexttable();
    goto nome_fornitore;
  }
} else if(mese_fattura != $mese_fine_conguaglio) {
  $calc_mese++;
  nexttable();
  goto nome_fornitore;
}
if(@ == "DATI FORNITURA") {
  $calc_conguaglio = 0;
  $calc_mese = 0;
  $next_page += 1 + $off_dettaglio;
  $off_dettaglio = 0;
  nexttable();
  clear $dettaglio;
  goto cerca_dettaglio;
}
### End Script
### End Box
