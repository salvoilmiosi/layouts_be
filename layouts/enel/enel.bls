### Bill Layout Script
### Flag Find Layout
### Language it_IT

### Box Convalida Layout
### Page 1
### Rect 0.03387097 0.096921325 0.34032258 0.044469774
### Script
if(!contains(@,"Enel Energia - Mercato libero dell'energia")) {
  error("Enel: Stringa di Convalida Errata", global ERRORE_LAYOUT_INVALIDO);
}
### End Script
### End Box

### Box Calcolo Offset Dettaglio
### Flags TRIM 
### Page 3
### Rect 0.053306885 0.20998158 0.3093039 0.027190598
### Goto Label calcolo_offset
### Spacers
p+$offset_dettaglio;
### End Spacers
### Script
if(ate()) {
  error("Enel: Dettaglio Mancante", global ERRORE_DETTAGLIO_MANCANTE);
} else if(@ != "DETTAGLIO IMPORTI IN BOLLETTA") {
  $offset_dettaglio++;
  goto calcolo_offset;
}
### End Script
### End Box

### Box Contenuto Dettaglio Costi
### Mode RAW
### Page 3
### Rect 0.048197635 0.21550742 0.71199036 0.76047003
### Goto Label dettaglio_costi
### Spacers
p+$offset_dettaglio;
t-$spacer_dettaglio;
### End Spacers
### Script
$dettaglio+=@;
### End Script
### End Box

### Box Spacer prima pagina dettaglio
### Flags SPACER 
### Page 3
### Rect 0.048348106 0.005701254 0.032232072 0.20980616
### Spacers
p+$offset_dettaglio;
### End Spacers
### Script
$spacer_dettaglio=box_height();
if(!ate()) {
  $offset_dettaglio++;
  goto dettaglio_costi;
}
### End Script
### End Box

### Box Nome Fornitore
### Flags NOREAD 
### Page 1
### Rect 0.038709678 0.030786773 0.22096774 0.061573543
### Goto Label nome_fornitore
### Script
fornitore="Enel Energia";
### End Script
### End Box

### Box Dati Fornitura
### Mode RAW
### Page 1
### Rect 0.04623656 0.1717978 0.41827956 0.16039528
### Script
indirizzo_fornitura=totitle(singleline(search(@,/Forniamo energia in ([^]+?\d{5}.+)\n/)));
potenza[0:3]=search_num(@,/(?:Potenza contrattualmente impegnata|Livello massimo di potenza) (\N)/);
### End Script
### End Box

### Box Ragione sociale
### Page 1
### Rect 0.4918691806451613 0.17783226572405927 0.4113566258064517 0.09012668524515394
### Script
ragione_sociale=totitle(search(@,/(.+)/));
### End Script
### End Box

### Box Numero cliente / POD
### Page 1
### Rect 0.05251878 0.33476514 0.20437472 0.07914594
### Script
numero_cliente=search(@,/N\u00b0 CLIENTE (.+)\n/);
codice_pod=search(@,/CODICE POD (\w+)/);
### End Script
### End Box

### Box Numero / Data fattura
### Page 1
### Rect 0.27857563 0.38391757 0.21629238 0.030719101
### Script
numero_fattura=search(@,/N\. Fattura (\d+)/);
data_fattura=search_date(@,"%d/%m/%Y",/Del (\D)/);
### End Script
### End Box

### Box Totale Fattura / Scadenza
### Page 1
### Rect 0.51906633 0.36861986 0.21763146 0.046880633
### Script
totale_fattura=search_num(@,/(\N)/);
data_scadenza=search_date(@,"%d/%m/%Y",/Entro il (\D)/);
### End Script
### End Box

### Box Periodo
### Page 1
### Rect 0.27988616 0.4306124 0.22106262 0.020122074
### Script
$mese_inizio=search_date(@,"%b. %Y");
$mese_fine=search_date(@,"%b. %Y",/\D - (\D)/);
if($mese_fine) {
  if($mese_fine < $mese_inizio) {
    error("Enel: Periodo Non Valido", global ERRORE_PERIODO_INVALIDO);
  }
}
mese_fattura=month_add($mese_inizio, $calc_mese);
### End Script
### End Box

### Box Dettaglio Fiscale
### Mode LAYOUT
### Page 1
### Rect 0.043979228 0.67284954 0.6890491 0.16947563
### Script
cts=search_num(@,/CORRISPETTIVO TARIFFARIO SPECIFICO.+ (\N)/);
iva=search(@,/IVA.* (\d+%)/);
### End Script
### End Box

### Box Consumi Rilevati
### Mode LAYOUT
### Flags PAGE 
### Page 2
### Rect 0.057569835 0.42134714 0.6868947 0.095169425
### Script
$inizio_dettaglio=date_format(last_day(month_add(mese_fattura,-1)),"%d/%m/%Y");
$fine_dettaglio=date_format(mese_fattura,"\\d\\d/%m/%Y");
if(!contains(@, $inizio_dettaglio)) {
  $inizio_dettaglio=date_format(mese_fattura,"%d/%m/%Y");
  $fine_dettaglio=date_format(last_day(mese_fattura),"%d/%m/%Y");
}
if(contains(@,"Energia attiva")) {
  (energia_attiva_rilevata[0], energia_attiva_rilevata[1], energia_attiva_rilevata[2])
    = captures_num(@,format(/$0 (\N) (\N) (\N)/, $fine_dettaglio));
  (energia_attiva_rilevata[0], energia_attiva_rilevata[1], energia_attiva_rilevata[2])
    -= captures_num(@,format(/$0 (\N) (\N) (\N)/, $inizio_dettaglio));
}
if(contains(@,"Energia reattiva")) {
  (energia_reattiva[0], energia_reattiva[1], energia_reattiva[2])
    = captures_num(@,format(/$0 \N \N \N (\N) (\N) (\N)/, $fine_dettaglio));
  (energia_reattiva[0], energia_reattiva[1], energia_reattiva[2])
    -= captures_num(@,format(/$0 \N \N \N (\N) (\N) (\N)/, $inizio_dettaglio));
}
### End Script
### End Box

### Box Parse Dettaglio Costi
### Flags NOREAD 
### Page 3
### Rect 0.0903738 0.0063137496 0.053058922 0.03473528
### Script
$fine_periodo=date_format(last_day(mese_fattura),"%d/%m/%y");
$regex_periodo=format(/(.+ dal \d\d\/\d\d\/\d\d al $0[^]+?)(?=\n.+ dal \d\d|Totale|$)/, $fine_periodo);
with($dettaglio) {
  with(between(@,"SPESA PER L'ENERGIA", "Totale spesa per l'energia")) {
    spesa_materia_energia=sum(matches_num(matches(@, $regex_periodo),/.+ (\N)/));
    
    (prezzo_energia[0], energia_attiva[0]) = captures_num(search(search(@,
      /Energia (?:fascia F1|ore picco|mese)\n([^]+?)Totale energia/), $regex_periodo),/\u20ac\/kWh (\N) (\N)/);
    (prezzo_energia[1], energia_attiva[1]) = captures_num(search(search(@,
      /Energia (?:fascia F2|ore fuori picco)\n([^]+?)Totale energia/), $regex_periodo),/\u20ac\/kWh (\N) (\N)/);
    (prezzo_energia[2], energia_attiva[2]) = captures_num(search(search(@,
      /Energia fascia F3\n([^]+?)Totale energia/), $regex_periodo),/\u20ac\/kWh (\N) (\N)/);

    with(between(@,"Altri importi materia energia", "Totale altri importi materia energia")) {
      with(str(matches(@, $regex_periodo))) {
        pcv=search_num(@,/Commercializzazione vendita .*\u20ac\/cliente\/ mese (\N)/);
        dispacciamento=search_num(@,/Dispacciamento .*\u20ac\/kWh (\N)/);
        sbilanciamento=search_num(@,/Corrispettivo di Sbilanciamento .+\u20ac\/kWh (\N)/);
      }
    }
  }
  with(between(@,/SPESA (?:PER IL )?TRASPORTO/,"Totale spesa")) {
    foreach(matches(@, $regex_periodo)) {
      trasporto_gestione+=sum(matches_num(@,/mesi.?1 (\N)/));
      trasporto_gestione+=sum(matches_num(@,/\/kWh \N \N (\N)/));
      trasporto_gestione+=sum(matches_num(@,/\/kVarh \N \N (\N)/));
      potenza[0:3]=search_num(@,/kW (\N)/);
    }
    with(between(@,"Quota energia")) {
      with(str(matches(@, $regex_periodo))) {
        penale_reattiva_inf75=search_num(@,/entro il 75% .*\u20ac\/kVarh \N \N (\N)/);
        penale_reattiva_sup75=search_num(@,/oltre il 75% .*\u20ac\/kVarh \N \N (\N)/);
      }
    }
  }
  with(between(@,"SPESA ONERI DI SISTEMA", "Totale spesa oneri di sistema")) {
    foreach(matches(@,format(/al $0([^]+?)(dal|Totale|$)/, $fine_periodo))) {
      oneri+=search_num(@,/mesi.?1 (\N)/);
      oneri+=search_num(@,/\/kWh \N \N (\N)/);
    }
  }
  with(between(@,"IMPOSTE E IVA", "Totale imposte e IVA")) {
    accise=search_num(@,format(/Accisa sull'energia elettrica \(entro 200000\) dal \d\d\/\d\d\/\d\d al $0 \u20ac\/kWh \N \N (\N)/, $fine_periodo));
  }
	
  imponibile = spesa_materia_energia + trasporto_gestione + oneri + accise;
}
### End Script
### End Box

### Box Prossima pagina
### Flags NOREAD 
### Page 3
### Rect 0.15071335 0.00890943 0.041901693 0.028506272
### Script
if($mese_fine) {
  if(mese_fattura != $mese_fine) {
    $calc_mese++;
    nexttable();
    goto nome_fornitore;
  }
}
### End Script
### End Box
