### Bill Layout Script
### Set Layout

### Box Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.03387097 0.096921325 0.34032258 0.044469774
### Script
$ifnot($contains(@,"Enel Energia - Mercato libero dell'energia")) {
  $error(*ERRORE_LAYOUT_INVALIDO, "Enel: Stringa di Convalida Errata");
}
### End Script
### End Box

### Box Calcolo Offset Dettaglio
### Type RECTANGLE
### Mode DEFAULT
### Flags TRIM 
### Page 3
### Rect 0.053306885 0.20998158 0.3093039 0.027190598
### Goto Label calcolo_offset
### Spacers
p+*offset_dettaglio;
### End Spacers
### Script
$if($sys.ate) {
  $error(*ERRORE_DETTAGLIO_MANCANTE, "Enel: Dettaglio Mancante");
} $elif(@ != "DETTAGLIO IMPORTI IN BOLLETTA") {
  *offset_dettaglio++;
  $goto calcolo_offset;
}
### End Script
### End Box

### Box Contenuto Dettaglio Costi
### Type RECTANGLE
### Mode RAW
### Page 3
### Rect 0.048197635 0.21550742 0.71199036 0.76047003
### Goto Label dettaglio_costi
### Spacers
p+*offset_dettaglio;
t-*spacer_dettaglio;
### End Spacers
### Script
*_dettaglio+=@;
### End Script
### End Box

### Box Spacer prima pagina dettaglio
### Type RECTANGLE
### Mode DEFAULT
### Flags SPACER 
### Page 3
### Rect 0.048348106 0.005701254 0.032232072 0.20980616
### Spacers
p+*offset_dettaglio;
### End Spacers
### Script
*spacer_dettaglio=$box.height;
$ifnot($sys.ate) {
  *offset_dettaglio++;
  $goto dettaglio_costi;
}
### End Script
### End Box

### Box Nome Fornitore
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD 
### Page 1
### Rect 0.038709678 0.030786773 0.22096774 0.061573543
### Goto Label nome_fornitore
### Script
fornitore="Enel Energia";
### End Script
### End Box

### Box Dati Fornitura
### Type RECTANGLE
### Mode RAW
### Page 1
### Rect 0.04623656 0.1717978 0.41827956 0.16039528
### Script
'indirizzo_fornitura=$singleline($search(@,/Forniamo energia in ([^]+?\d{5}.+)\n/));
%potenza[0:3]=$search(@,/(?:Potenza contrattualmente impegnata|Livello massimo di potenza) (\N)/);
### End Script
### End Box

### Box Ragione sociale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.4918691806451613 0.17783226572405927 0.4113566258064517 0.09012668524515394
### Script
$setend($indexof(@,"\n"));
.'ragione_sociale;
### End Script
### End Box

### Box Numero cliente / POD
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.05251878 0.33476514 0.20437472 0.07914594
### Script
numero_cliente=$search(@,/N\u00b0 CLIENTE (.+)\n/);
codice_pod=$search(@,/CODICE POD (\w+)/);
### End Script
### End Box

### Box Numero / Data fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.27857563 0.38391757 0.21629238 0.030719101
### Script
numero_fattura=$search(@,/N\. Fattura (\N)/);
data_fattura=$date(@,"%d/%m/%Y",/Del (\D)/);
### End Script
### End Box

### Box Totale Fattura / Scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.51906633 0.36861986 0.21763146 0.046880633
### Script
%totale_fattura=$search(@,/(\N)/);
data_scadenza=$date(@,"%d/%m/%Y",/Entro il (\D)/);
### End Script
### End Box

### Box Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.27988616 0.4306124 0.22106262 0.020122074
### Script
mese_fattura=$month(@,"%b. %Y");
mese_fine=$month(@,"%b. %Y",/\D - (\D)/);
$if(mese_fine) {
  $if(mese_fine < mese_fattura) {
    $error(*ERRORE_PERIODO_INVALIDO, "Enel: Periodo Non Valido");
  }
  mese_fattura=$month_add(mese_fattura,*calc_mese);
}
### End Script
### End Box

### Box Dettaglio Fiscale
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### Rect 0.043979228 0.67284954 0.6890491 0.16947563
### Script
%cts=$search(@,/CORRISPETTIVO TARIFFARIO SPECIFICO.+ (\N)/);
iva=$search(@,/IVA.* (\N%)/);
### End Script
### End Box

### Box Consumi Rilevati
### Type PAGE
### Mode LAYOUT
### Page 2
### Rect 0.057569835 0.42134714 0.6868947 0.095169425
### Script
_inizio_dettaglio=$date_format($last_day($month_add(mese_fattura,-1)),"%d/%m/%Y");
_fine_dettaglio=$date_format(mese_fattura,"\\d\\d/%m/%Y");
$ifnot($contains(@,_inizio_dettaglio)) {
  _inizio_dettaglio=$date_format(mese_fattura,"%d/%m/%Y");
  _fine_dettaglio=$date_format($last_day(mese_fattura),"%d/%m/%Y");
}
$if($contains(@,"Energia attiva")) {
  $step($captures(@,$format(/$0 (\N) (\N) (\N)/,_fine_dettaglio))) {
    .%energia_attiva_rilevata[0]; .%energia_attiva_rilevata[1]; .%energia_attiva_rilevata[2];
  }
  $step($captures(@,$format(/$0 (\N) (\N) (\N)/,_inizio_dettaglio))) {
    %energia_attiva_rilevata[0]-=@; %energia_attiva_rilevata[1]-=@; %energia_attiva_rilevata[2]-=@;
  }
}
$if($contains(@,"Energia reattiva")) {
  $step($captures(@,$format(/$0 \N \N \N (\N) (\N) (\N)/,_fine_dettaglio))) {
    .%energia_reattiva[0]; .%energia_reattiva[1]; .%energia_reattiva[2];
  }
  $step($captures(@,$format(/$0 \N \N \N (\N) (\N) (\N)/,_inizio_dettaglio))) {
    %energia_reattiva[0]-=@; %energia_reattiva[1]-=@; %energia_reattiva[2]-=@;
  }
}
### End Script
### End Box

### Box Parse Dettaglio Costi
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD 
### Page 3
### Rect 0.0903738 0.0063137496 0.053058922 0.03473528
### Script
_fine_periodo = $date_format($last_day(mese_fattura),"%d/%m/%y");
_regex_periodo = $format(/(.+ dal \d\d\/\d\d\/\d\d al $0[^]+?)(?=\n.+ dal \d\d|Totale|$)/, _fine_periodo);
$with(*_dettaglio) {
  $between("SPESA PER L'ENERGIA", "Totale spesa per l'energia") {
    ^spesa_materia_energia=$search_all($search_all(@,_regex_periodo),/.+ (\N)/);
    $step($captures($search($search(@, /Energia (?:fascia F1|ore picco|mese)\n([^]+?)Totale energia/),
      _regex_periodo),/\u20ac\/kWh (\N) (\N)/))
    {
      .%prezzo_energia[0]; .%energia_attiva[0];
    }
    $step($captures($search($search(@, /Energia (?:fascia F2|ore fuori picco)\n([^]+?)Totale energia/),
      _regex_periodo),/\u20ac\/kWh (\N) (\N)/))
    {
      .%prezzo_energia[1]; .%energia_attiva[1];
    }
    $step($captures($search($search(@, /Energia fascia F3\n([^]+?)Totale energia/),
      _regex_periodo),/\u20ac\/kWh (\N) (\N)/))
    {
      .%prezzo_energia[2]; .%energia_attiva[2];
    }
    $between("Altri importi materia energia", "Totale altri importi materia energia") {
      $with($search_all(@,_regex_periodo)) {
        %pcv=$search(@,/Commercializzazione vendita .*\u20ac\/cliente\/ mese (\N)/);
        %dispacciamento=$search(@,/Dispacciamento .*\u20ac\/kWh (\N)/);
        %sbilanciamento=$search(@,/Corrispettivo di Sbilanciamento .+\u20ac\/kWh (\N)/);
      }
    }
  }
  $newview {
    $setbegin($searchposend(@,/SPESA (?:PER IL )?TRASPORTO/));
    $setend($indexof(@,"Totale spesa"));
    $foreach($search_all(@,_regex_periodo)) {
      ^trasporto_gestione+=$search_all(@,/mesi.?1 (\N)/);
      ^trasporto_gestione+=$search_all(@,/\/kWh \N \N (\N)/);
      ^trasporto_gestione+=$search_all(@,/\/kVarh \N \N (\N)/);
      %potenza[0:3]=$search(@,/kW (\N)/);
    }
    $between("Quota energia") {
      $with($search_all(@,_regex_periodo)) {
        %penale_reattiva_inf75=$search(@,/entro il 75% .*\u20ac\/kVarh (\N)/);
        %penale_reattiva_sup75=$search(@,/oltre il 75% .*\u20ac\/kVarh (\N)/);
      }
    }
  }
  $between("SPESA ONERI DI SISTEMA", "Totale spesa oneri di sistema") {
    $foreach($search_all(@,$format(/al $0([^]+?)(dal|Totale|$)/,_fine_periodo))) {
      %oneri+=$search(@,/mesi.?1 (\N)/);
      %oneri+=$search(@,/\/kWh \N \N (\N)/);
    }
  }
  $between("IMPOSTE E IVA", "Totale imposte e IVA") {
    %accise=$search(@,$format(/Accisa sull'energia elettrica \(entro 200000\) dal \d\d\/\d\d\/\d\d al $0 \u20ac\/kWh \N \N (\N)/,_fine_periodo));
  }
	
  imponibile=$sum(spesa_materia_energia, trasporto_gestione, oneri, accise);
}
### End Script
### End Box

### Box Prossima pagina
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD 
### Page 3
### Rect 0.15071335 0.00890943 0.041901693 0.028506272
### Script
$if(mese_fine) {
  $if(mese_fattura != mese_fine) {
    *calc_mese++;
    $nexttable;
    $goto nome_fornitore;
  }
}
### End Script
### End Box
