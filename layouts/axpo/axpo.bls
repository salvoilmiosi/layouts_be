### Bill Layout Script
### Set Layout
### Language it_IT

### Box Convalida Layout
### Flags TRIM 
### Page 1
### Rect 0.78485096 0.023945268 0.20306204 0.07639681
### Goto Label check_convalida
### Spacers
p+$off_allegato;
### End Spacers
### Script
if(ate()) {
  error("Axpo: Stringa di Convalida Errata", global ERRORE_LAYOUT_INVALIDO);
} else if(@ != "BOLLETTA\nENERGIA\nELETTRICA\nFornitura del Mercato Libero") {
  $off_allegato++;
  goto check_convalida;
}
### End Script
### End Box

### Box Check No Dettaglio
### Flags TRIM 
### Page 2
### Rect 0.037096774193548385 0.09578107183580388 0.22741935483870968 0.026225769669327242
### Spacers
p+$off_allegato;
### End Spacers
### Script
if(@ == "COMUNICAZIONI") {
  error("Axpo: Dettaglio Mancante", global ERRORE_DETTAGLIO_MANCANTE);
}
### End Script
### End Box

### Box Prima Pagina Dettaglio
### Flags TRIM 
### Page 3
### Rect 0.0379746835443038 0.0894854586129754 0.3069620253164557 0.03131991051454139
### Goto Label cerca_dettaglio
### Spacers
p+$off_allegato;
p+$off_dettaglio;
p+$next_page;
### End Spacers
### Script
if(ate()) {
  error("Axpo: Dettaglio Mancante", global ERRORE_DETTAGLIO_MANCANTE);
}else if(@ != "QUADRO DI DETTAGLIO") {
  $off_dettaglio++;
  goto cerca_dettaglio;
}
### End Script
### End Box

### Box Lettura Dettaglio
### Mode RAW
### Page 3
### Rect 0.028008772215297406 0.08907881299899899 0.95483875 0.8556215434660633
### Goto Label lettura_dettaglio
### Spacers
p+$off_allegato;
p+$off_dettaglio;
p+$next_page;
### End Spacers
### Script
if (ate()) {
  if (! $dettaglio) {
    error("Axpo: Dettaglio Mancante", global ERRORE_DETTAGLIO_MANCANTE);
  }
} else if (!(contains(@,"DATI FORNITURA") || contains(@,"COMUNICAZIONI"))) {
  $dettaglio+=@;
  $off_dettaglio++;
  goto lettura_dettaglio;
}
### End Script
### End Box

### Box Nome Fornitore
### Flags NOREAD 
### Page 1
### Rect 0.020967742 0.021664767 0.23548387 0.069555305
### Goto Label nome_fornitore
### Script
fornitore="Axpo";
### End Script
### End Box

### Box Ragione sociale
### Page 1
### Rect 0.51044726 0.16909401 0.4719575 0.11482845294184721
### Script
ragione_sociale=totitle(search(@,/(.+)/));
### End Script
### End Box

### Box Totale / scadenza
### Page 1
### Rect 0.5112903225806451 0.3080678 0.459281297419355 0.2258688515837104
### Spacers
p+$off_allegato;
### End Spacers
### Script
totale_fattura=search_num(@,/TOTALE BOLLETTA: (\N)/);
data_scadenza=search_date(@,"%d.%m.%Y",/entro il (\D)/);
### End Script
### End Box

### Box Dati bolletta
### Page 1
### Rect 0.0315568 0.09791284 0.22615707 0.31941897
### Spacers
p+$off_allegato;
### End Spacers
### Script
numero_fattura=search(@,/N\. fattura (\N)/);
data_fattura=search_date(@,"%d.%m.%Y",/N\. fattura \N del (\D)/);
numero_cliente=search(@,/CODICE CLIENTE (.+)\n/);
### End Script
### End Box

### Box Parsing Dettaglio
### Flags NOREAD 
### Page 3
### Rect 0.21504012 0.053452756 0.04193318 0.029393759
### Script
function calc_prezzo_energia(@, $expr) {
  foreach(matches(@,format(/al \d\d.$0 Euro\/kWh (\N \N \N)/, $expr))) {
    step(captures_num(@,/(\N) (\N) (\N)/)) {
      $tot_unitario=@;
      $tot_kwh+=@;
      $tot_prezzo+=@;
    }
    $i++;
  }
  if($i == 1) return list($tot_unitario, $tot_kwh);
  else if($tot_prezzo) return list(trunc($tot_prezzo / $tot_kwh,6), $tot_kwh);
}

with($dettaglio) {
  codice_pod=search(@,/CODICE POD ([0-9A-Z]+)/);
  indirizzo_fornitura=totitle(singleline(search(@,/FORNIAMO ENERGIA IN\n([^]+?\d{5}.+)/)));

  $mese_inizio=search_month(@,"%d.%m.%Y",/PERIODO (\D) - \D/);
  $mese_fine=search_month(@,"%d.%m.%Y",/PERIODO \D - (\D)/);
  if($mese_fine < $mese_inizio) {
    error("Axpo: Periodo Non Valido", global ERRORE_PERIODO_INVALIDO);
  }
  mese_fattura=month_add($mese_inizio, $calc_mese);
  $mese=date_format(mese_fattura,"%m\\.%Y");

  iva=search(@,/(\d+%)\n/);

  with(between(@,"SPESA PER LA MATERIA ENERGIA","Subtotale")) {
    spesa_materia_energia=sum(matches_num(@,format(/dal \d\d\.\d\d\.\d\d\d\d al \d\d\.$0.+ (\N) \d+%/, $mese)));
    sbilanciamento=sum(matches_num(search(@,/Sbilanciamento\n((dal.+\n)+)/),
      format(/al \d\d\.$0 Euro\/kWh (\N)/, $mese)));
    pcv=sum(matches_num(search(@,/Prezzo Commercializzazione Vendita\n((dal.+\n)+)/),
      format(/al \d\d\.$0 euro\/\w+ \N \N (\N)/, $mese)));
    with(search(@,/Prezzo (?:Energia|Indicizzato)\n((dal.+\n)+)/)) {
      step(calc_prezzo_energia(format(/$0 (?:F1|PEAK)/, $mese))) {
        prezzo_energia[0]=@;
        energia_attiva[0]=@;
      }
      step(calc_prezzo_energia(format(/$0 (?:F2|OFFPEAK)/, $mese))) {
        prezzo_energia[1]=@;
        energia_attiva[1]=@;
      }
      step(calc_prezzo_energia(format(/$0 F3/, $mese))) {
        prezzo_energia[2]=@;
        energia_attiva[2]=@;
      }
      step(calc_prezzo_energia($mese)) {
        prezzo_energia=@;
        energia_attiva=@;
      }
    }
  }
  with(between(@,"SCONTI","Subtotale")) {
    sconti=sum(matches_num(@,format(/dal \d\d\.\d\d\.\d\d\d\d al \d\d\.$0.+ (\N) \d+%/, $mese)));
  }
  with(between(@,"SPESA PER IL TRASPORTO","Subtotale")) {
    trasporto_gestione=sum(matches_num(@,format(/dal \d\d\.\d\d\.\d\d\d\d al \d\d\.$0.+ (\N) \d+%/, $mese)));
    with(between(@,"QUOTA POTENZA","QUOTA ENERGIA")) {
      potenza[0:3]=sum(matches_num(@,format(/al \d\d\.$0 Euro\/kW \N (\N)/, $mese)));
    }

    penale_reattiva_inf75=sum(matches_num(search(@,/Energia Reattiva entro 75%\n((dal.+\n)+)/),
      format(/al \d\d\.$0 F[1-3] Euro\/kVarh \N \N (\N)/, $mese)));
    penale_reattiva_sup75=sum(matches_num(search(@,/Energia Reattiva oltre 75%\n((dal.+\n)+)/),
      format(/al \d\d\.$0 F[1-3] Euro\/kVarh \N \N (\N)/, $mese)));
  }
  with(between(@,"ONERI DI SISTEMA","Subtotale")) {
    oneri=sum(matches_num(@,format(/dal \d\d\.\d\d\.\d\d\d\d al \d\d\.$0.+ (\N) \d+%/, $mese)));
  }
  with(between(@,"IMPOSTE","Subtotale")) {
    accise=sum(matches_num(matches(@,/Imposta erariale.*\n((dal.+\n)+)/),
      format(/al \d\d\.$0 Euro\/kWh \N \N (\N)/, $mese)));
  }

  imponibile = spesa_materia_energia + sconti + trasporto_gestione + oneri +accise;
}
### End Script
### End Box

### Box Tabella Consumi
### Mode LAYOUT
### Flags PAGE 
### Page 2
### Rect 0.21568559774193555 0.047890542645381985 0.03915311193548382 0.03078676636259977
### Spacers
p+$off_allegato;
p+$next_page;
### End Spacers
### Script
with(between(@,"CONSUMI E LETTURE","Consumi fatturati negli ultimi 12 mesi")) {
  foreach(matches(@,/(.*Periodo \(dal - al\).+\n.+(?:\n.*\d{2}.\d{2}.\d{4}.+)+)/)) {
    $header=table_header(@,/Consumi fatturati kWh(?: Consumi fatturati kWh)?/,"Energia Reattiva");
    foreach(matches(@,format(/(.+ - \d\d\.$0.+)/, $mese))) {
      step(table_row(@, $header)) {
        step(captures_num(@,/(\N) (\N) (\N)/)) {
          energia_attiva_rilevata[0]+=@;
          energia_attiva_rilevata[1]+=@;
          energia_attiva_rilevata[2]+=@;
        }
        energia_reattiva += +@;
      }
    }
  }
}
### End Script
### End Box

### Box Prossima Pagina
### Flags TRIM 
### Page 3
### Rect 0.03840065 0.0989254 0.22611547 0.032341655
### Spacers
p+$off_allegato;
p+$next_page;
p+$off_dettaglio;
### End Spacers
### Script
if(mese_fattura != $mese_fine) {
  # Prossima tabella multimese
  $calc_mese++;
  nexttable();
  goto nome_fornitore;
}
if(@ == "DATI FORNITURA") {
  # Prossima tabella multipod
  $calc_mese = 0;
  $next_page += 1 + $off_dettaglio;
  $off_dettaglio = 0;
  nexttable();
  clear $dettaglio;
  goto cerca_dettaglio;
}
### End Script
### End Box
