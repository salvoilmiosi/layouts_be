### Bill Layout Script
### Set Layout

### Box Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Flags TRIM 
### Page 1
### Rect 0.5398872 0.030786771 0.3545528 0.05587229
### Script
$if(@ != "Bolletta Energia Elettrica\nFornitura del Mercato Libero")
  $error(*ERRORE_LAYOUT_INVALIDO, "Axpo (old): Stringa di Convalida Errata");
### End Script
### End Box

### Box Nome Fornitore
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD 
### Page 1
### Rect 0.024193548 0.014823261 0.19271415 0.061848786
### Goto Label nome_fornitore
### Script
fornitore="Axpo";
### End Script
### End Box

### Box Numero fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.03548387 0.18015963 0.47258064 0.033067275
### Script
numero_fattura=$search(@,/Fattura N\u00b0 (\N)/);
data_fattura=$date(@,"%d.%m.%Y",/del (\D)/);
### End Script
### End Box

### Box Scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.54147464 0.095020905 0.43266767 0.04615301
### Script
data_scadenza=$date(@,"%d.%m.%Y");
### End Script
### End Box

### Box Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.03548387 0.23147093 0.20483871 0.031927023
### Script
mese_fattura=$month(@,"%B %Y");
### End Script
### End Box

### Box Totale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.2435484 0.2599772 0.2564516 0.03990878
### Script
.%totale_fattura;
### End Script
### End Box

### Box Nome Cliente
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### Rect 0.021505376 0.10642341 0.49462366 0.06176359
### Script
numero_cliente=$search(@,/Codice Cliente: (\N)/);
'ragione_sociale=$search(@,/.+\n(.+)\n/);
partita_iva=$search(@,/Part\.IVA (.+)\n/);
### End Script
### End Box

### Box Dati fornitura singolo pod
### Type RECTANGLE
### Mode RAW
### Page 1
### Rect 0.067204304 0.61503536 0.42183626 0.30491322
### Script
$if($contains(@,"Dati di fornitura")) {
  'indirizzo_fornitura=$singleline($search(@,/Indirizzo ([^]+?\(..\))/));
  codice_pod=$search(@,/POD (.+)\n/);
  %potenza[0:3]=$search(@,/Potenza Impegnata (\N)/);
} $else {
  *spacer_multipod=1;
}
### End Script
### End Box

### Box Sintesi dati singolo pod
### Type RECTANGLE
### Mode RAW
### Page 1
### Rect 0.06617039400000002 0.3263163991448119 0.42907363 0.2335468
### Script
iva=$search(@,/IVA (\N%)/);
$ifnot(*spacer_multipod) {
  %spesa_materia_energia=$search(@,/(?:Corrispettivi energia|Spese per l'energia) (\N) \u20ac/);
  %trasporto_gestione=$search(@,/(?:Trasporto|Spesa per trasporto, gestione contatore e oneri di sistema) (\N) \u20ac/);
}
### End Script
### End Box

### Box Dati fornitura multipod
### Type RECTANGLE
### Mode RAW
### Page 3
### Rect 0.03205999 0.09432077 0.45019805 0.24091297
### Goto Label dati_fattura_multipod
### Spacers
p+*next_page;
### End Spacers
### Script
$if(*spacer_multipod) {
  'indirizzo_fornitura=$singleline($search(@,/Indirizzo ([^]+?\(..\))/));
  codice_pod=$search(@,/POD (.+)\n/);
  %potenza[0:3]=$search(@,/Potenza Impegnata (\N)/);
}
### End Script
### End Box

### Box Sintesi dati multipod
### Type RECTANGLE
### Mode RAW
### Page 3
### Rect 0.4935484 0.09236032 0.45645162 0.16419612
### Spacers
p+*next_page;
### End Spacers
### Script
$if(*spacer_multipod) {
  %spesa_materia_energia=$search(@,/(?:Corrispettivi energia|Spese per l'energia) \u20ac (\N)/);
  %trasporto_gestione=$search(@,/(?:Trasporto|Spesa per trasporto, gestione contatore e oneri di sistema) \u20ac (\N)/);
}
### End Script
### End Box

### Box Lettura Spese Energia
### Type RECTANGLE
### Mode LAYOUT
### Page 3
### Rect 0.033843674 0.09464082 0.9073328 0.87001145
### Goto Label lettura_spese
### Spacers
p+*spacer_multipod;
p+*next_page;
### End Spacers
### Script
$if($sys.ate || $contains(@,"Comunicazioni") || $contains(@,"Dati di fornitura")) {
  $goto parsing_spese;
}
_dettaglio+=@;
*next_page++;
$goto lettura_spese;
### End Script
### End Box

### Box Parsing Spese Energia
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD 
### Page 3
### Rect 0.19357695 0.047890533 0.046551988 0.030786768
### Goto Label parsing_spese
### Script
$with(_dettaglio) {
  $setbegin($indexofend(@,$date_format(mese_fattura,"%B %Y")));
  $setend($searchpos(@,/\n.+?TOT\. \u20ac/));

  $between("Consumi rilevati dal distributore","Spese per l'energia") {
    %energia_attiva_rilevata[0]=$search(@,/F1 .+ (\N)\n/);
    %energia_attiva_rilevata[1]=$search(@,/F2 .+ (\N)\n/);
    %energia_attiva_rilevata[2]=$search(@,/F3 .+ (\N)\n/);
  }

  %imponibile=$search(@,/TOT\. \u20ac (\N)/);

  $step($captures(@,/Prezzo Energia (?:F1|PEAK) (\N) kWh (\N)/)) {
    .%energia_attiva[0]; .%prezzo_energia[0];
  }
  $step($captures(@,/Prezzo Energia (?:F2|OFFPEAK) (\N) kWh (\N)/)) {
    .%energia_attiva[1]; .%prezzo_energia[1];
  }
  $step($captures(@,/Prezzo Energia F3 (\N) kWh (\N)/)) {
    .%energia_attiva[2]; .%prezzo_energia[2];
  }

  %pcv=$search(@,/Prezzo Commercializzazione e Vendita .+\u20ac (\N)/);
  %sbilanciamento=$search(@,/Gestione sbilanciamento \N kWh (\N)/);
  %potenza[0:3]=$search(@,/Quota Potenza (\N) kW/);

  ^penale_reattiva_inf75=$search_all(@,/Energia Reattiva tra il 33 e il 75%(?: F[1-3])?.+ (\N)\n/);
  ^penale_reattiva_sup75=$search_all(@,/Energia Reattiva oltre il 75%(?: F[1-3])?.+ (\N)\n/);

  %accise=$search(@,/Imposta Erariale .+ \u20ac\/kWh.+ (\N)\n/);
}
### End Script
### End Box

### Box Controllo Prossima pagina
### Type RECTANGLE
### Mode DEFAULT
### Flags TRIM 
### Page 3
### Rect 0.040290087 0.09806158 0.20306204 0.029646521
### Spacers
p+*spacer_multipod;
p+*next_page;
### End Spacers
### Script
$if(*spacer_multipod) {
  $if(@ == "Dati di fornitura") {
    *next_page++;
    $nexttable;
    $goto nome_fornitore;
  }
}
### End Script
### End Box
