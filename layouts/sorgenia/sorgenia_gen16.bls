### Bill Layout Script
### Set Layout

### Box Convalida Layout
### Type RECTANGLE
### Mode DEFAULT
### Flags TRIM
### Page 1
### Rect 0.037950665 0.5240123 0.4743833 0.03269837
### Script
$if(@ != "Servizio di fornitura energia elettrica") {
  $error(*ERRORE_LAYOUT_INVALIDO, "Sorgenia (gen16): Stringa di Convalida Errata");
}
### End Script
### End Box

### Box Nome Fornitore
### Type RECTANGLE
### Mode DEFAULT
### Flags NOREAD
### Page 1
### Rect 0.043548387 0.034207527 0.3548387 0.08437856
### Goto Label nome_fornitore
### Script
fornitore="Sorgenia";
### End Script
### End Box

### Box Codice Cliente
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.045161292 0.21664767 0.40967742 0.021664754
### Script
numero_cliente=$search(@,/Codice Cliente (.+)/);
### End Script
### End Box

### Box Ragione Sociale
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.49973556 0.19253415 0.408514 0.08879002
### Script
$setend($indexof(@,"\n"));
.'ragione_sociale;
### End Script
### End Box

### Box Dati fornitura
### Type RECTANGLE
### Mode LAYOUT
### Page 1
### Rect 0.03701745 0.30936313 0.42570066 0.20374973
### Script
codice_pod=$search(@,/POD \(Punto di prelievo\): (.+)/);
'indirizzo_fornitura=$singleline($search(@,/Indirizzo di fornitura:\n([^]+?\d{5}.+)/));
%potenza[0:3]=$search(@,/(\N) kW di potenza impegnata/);
### End Script
### End Box

### Box Periodo
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.26176625 0.5822756 0.23662084 0.025235059
### Script
mese_fattura=$month(@,"%B %Y");
$ifnot(mese_fattura) {
  mese_fattura=$month(@,"%b %Y",/(\D) - \D/);
  mese_fine=$month(@,"%b %Y",/\D - (\D)/);
}
$ifnot(mese_fattura) {
  _anno=$search(@,/(\d\d\d\d)/);
  mese_fattura=$month($format("$0 $1",_anno,$search(@,/(\w\w\w) - \w\w\w/)),"%Y %b");
  mese_fine=$month($format("$0 $1",_anno,$search(@,/\w\w\w - (\w\w\w)/)),"%Y %b");
}
$if(mese_fine) {
  $if(mese_fine < mese_fattura) {
    $error(*ERRORE_PERIODO_INVALIDO, "Sorgenia: Periodo Non Valido");
  }
  mese_fattura=$month_add(mese_fattura,*calc_mese);
}
### End Script
### End Box

### Box Data Scadenza
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.50898993 0.55890983 0.39132738 0.09159392
### Script
data_scadenza=$date(@,"%d/%m/%Y");
numero_fattura=$search(@,/Fattura n\u00b0 (.+)/);
data_fattura=$date(@,"%d/%m/%Y",/Del: (\D)/);
### End Script
### End Box

### Box Totale Fattura
### Type RECTANGLE
### Mode DEFAULT
### Page 1
### Rect 0.26176625 0.55984443 0.20227392 0.023365796
### Script
%totale_fattura=$search(@,/euro (\N)/);
### End Script
### End Box

### Box Dettaglio
### Type RECTANGLE
### Mode LAYOUT
### Page 2
### Rect 0.0315568 0.02850627 0.9274193 0.91467947
### Script
$setbegin($indexofend(@,$date_format(mese_fattura,"%B %Y")));
$setend($indexof(@,"\n",$indexof(@,"FORNITURA DI")));

$step($captures(@,/CONSUMI EFFETTIVI \(kWh\) (\N) (\N) (\N)/)) {
  .%energia_attiva_rilevata[0]; .%energia_attiva_rilevata[1]; .%energia_attiva_rilevata[2];
}
%spesa_materia_energia=$search(@,/SERVIZI DI VENDITA (\N)/);
%pcv=$search(@,/PCV euro\/cliente\/mese \N \N (\N)/);
$step($captures(@,/Prezzo F1 euro\/kWh (\N) (\N)/)) {
  .%prezzo_energia[0]; .%energia_attiva[0];
}
$step($captures(@,/Prezzo F2 euro\/kWh (\N) (\N)/)) {
  .%prezzo_energia[1]; .%energia_attiva[1];
}
$step($captures(@,/Prezzo F3 euro\/kWh (\N) (\N)/)) {
  .%prezzo_energia[2]; .%energia_attiva[2];
}
%trasporto_gestione=$search(@,/SERVIZI DI RETE (\N)/);
%potenza[0:3]=$search(@,/Quota Potenza euro\/kW\/mese \N (\N)/);
%accise=$search(@,/Imposta erariale \(fino a .+ kWh\) euro\/kWh \N \N (\N)/);
%imponibile=$search(@,/(\N)$/);
### End Script
### End Box

### Box Prossima Pagina
### Type RECTANGLE
### Mode DEFAULT
### Page 2
### Rect 0.036764704 0.0058689383 0.030834913 0.017606815
### Script
$if(mese_fine) {
  $if(mese_fattura != mese_fine) {
    *calc_mese++;
    $nexttable;
    $goto nome_fornitore;
  }
}
### End Script
### End Box
