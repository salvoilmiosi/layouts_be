### Bill Layout Script
### Set Layout
### Language it_IT

### Box Convalida Layout
### Flags TRIM 
### Page 1
### Rect 0.037950665 0.5240123 0.4743833 0.03269837
### Script
if(@ != "Servizio di fornitura energia elettrica") {
  error("Sorgenia (gen16): Stringa di Convalida Errata", global ERRORE_LAYOUT_INVALIDO);
}
### End Script
### End Box

### Box Nome Fornitore
### Flags NOREAD 
### Page 1
### Rect 0.043548387 0.034207527 0.3548387 0.08437856
### Goto Label nome_fornitore
### Script
fornitore="Sorgenia";
### End Script
### End Box

### Box Codice Cliente
### Page 1
### Rect 0.045161292 0.21664767 0.40967742 0.021664754
### Script
numero_cliente=search(@,/Codice Cliente (.+)/);
### End Script
### End Box

### Box Ragione Sociale
### Page 1
### Rect 0.49973556 0.19253415 0.408514 0.08879002
### Script
'ragione_sociale=search(@,/(.+)/);
### End Script
### End Box

### Box Dati fornitura
### Mode LAYOUT
### Page 1
### Rect 0.03701745 0.30936313 0.42570066 0.20374973
### Script
codice_pod=search(@,/POD \(Punto di prelievo\): (.+)/);
'indirizzo_fornitura=singleline(search(@,/Indirizzo di fornitura:\n([^]+?\d{5}.+)/));
%potenza[0:3]=search(@,/(\N) kW di potenza impegnata/);
### End Script
### End Box

### Box Periodo
### Flags TRIM 
### Page 1
### Rect 0.26176625 0.5822756 0.23662084 0.025235059
### Script
if(matches(@,/\w+ - \w+ \d{4}/)) {
  _items=captures(@,/(\w+) - (\w+) (\d{4})/);
  mese_inizio=date(format("$0 $1", _items[0], _items[2]), "%b %Y");
  mese_fine=date(format("$0 $1", _items[1], _items[2]), "%b %Y");
}else if(matches(@,/\w+ \d{4} - \w+ \d{4}/)) {
  mese_inizio=search_date(@,"%b %Y",/(\D) - \D/);
  mese_fine=search_date(@,"%b %Y",/\D - (\D)/);
}else if(matches(@,/\w+ \d{4}/)) {
  mese_inizio=date(@,"%B %Y");
  mese_fine=date(@,"%B %Y");
}else{
  error("Sorgenia: Formato Periodo Non Valido", global ERRORE_PERIODO_INVALIDO);
}
mese_fattura=month_add(mese_inizio,global calc_mese);
### End Script
### End Box

### Box Data Scadenza
### Page 1
### Rect 0.50898993 0.55890983 0.39132738 0.09159392
### Script
data_scadenza=search_date(@,"%d/%m/%Y");
numero_fattura=search(@,/Fattura n\u00b0 (.+)/);
data_fattura=search_date(@,"%d/%m/%Y",/Del: (\D)/);
### End Script
### End Box

### Box Totale Fattura
### Page 1
### Rect 0.26176625 0.55984443 0.20227392 0.023365796
### Script
%totale_fattura=search(@,/euro (\N)/);
### End Script
### End Box

### Box Dettaglio
### Mode LAYOUT
### Page 2
### Rect 0.0315568 0.02850627 0.9274193 0.91467947
### Script
with(rbetween(@,date_format(mese_fattura,"%B %Y"),/FORNITURA DI .+/)) {
  step(captures(@,/CONSUMI EFFETTIVI \(kWh\) (\N) (\N) (\N)/)) {
    set %energia_attiva_rilevata[0];
    set %energia_attiva_rilevata[1];
    set %energia_attiva_rilevata[2];
  }
  %spesa_materia_energia=search(@,/SERVIZI DI VENDITA (\N)/);
  %pcv=search(@,/PCV euro\/cliente\/mese \N \N (\N)/);
  step(captures(@,/Prezzo F1 euro\/kWh (\N) (\N)/)) {
    set %prezzo_energia[0];
    set %energia_attiva[0];
  }
  step(captures(@,/Prezzo F2 euro\/kWh (\N) (\N)/)) {
    set %prezzo_energia[1];
    set %energia_attiva[1];
  }
  step(captures(@,/Prezzo F3 euro\/kWh (\N) (\N)/)) {
    set %prezzo_energia[2];
    set %energia_attiva[2];
  }
  %trasporto_gestione=search(@,/SERVIZI DI RETE (\N)/);
  %potenza[0:3]=search(@,/Quota Potenza euro\/kW\/mese \N (\N)/);
  %accise=search(@,/Imposta erariale \(fino a .+ kWh\) euro\/kWh \N \N (\N)/);
  %imponibile=search(@,/(\N)$/);
}
### End Script
### End Box

### Box Prossima Pagina
### Page 2
### Rect 0.036764704 0.0058689383 0.030834913 0.017606815
### Script
if(mese_fattura != mese_fine) {
  global calc_mese++;
  nexttable();
  goto nome_fornitore;
}
### End Script
### End Box
