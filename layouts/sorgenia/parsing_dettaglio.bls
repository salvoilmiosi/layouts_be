### Bill Layout Script
### Language it_IT

### Box Parsing Dettaglio
### Flags NOREAD
### Page 1
### Rect 0 0 0 0
### Script
function calc_prezzo_energia(@, $expr) {
  foreach(matches(@,format(/Prezzo $0 euro\/kWh ($1 $1 $1)/, $expr, number_regex()))) {
    with(captures_num(@,/(\N) (\N) (\N)/)) {
      $tot_unitario=@[0];
      $tot_kwh+=@[1];
      $tot_prezzo+=@[2];
    }
    $i++;
  }
  if($i == 1) return list($tot_unitario, $tot_kwh);
  else if($tot_prezzo) return list(trunc($tot_prezzo/$tot_kwh,6), $tot_kwh);
}

with(global dettaglio) {
  with(between(@,"CONSUMI e RICALCOLI","CONSUMI ULTIMI 13 MESI")) {
    energia_attiva_rilevata=captures_num(between(@,"ENERGIA ATTIVA","ENERGIA REATTIVA"),
      format(/$0 \w+\*? (\N) (\N) (\N)/,date_format(mese_fattura,"%b - %y")));
    energia_reattiva=captures_num(between(@,"ENERGIA REATTIVA"),
      format(/$0 (\N) (\N) (\N)/,date_format(mese_fattura,"%b - %y")));
  }
  
  cts=sum(matches_num(@,/Corrispettivo Tariffario Specifico dal \d\d\/\d\d\/\d\d\d\d al \d\d\/\d\d\/\d\d\d\d (\N)/));
  iva=search(@,/Iva (\d+%)/);

  with(rbetween(@,date_format(mese_fattura,"%B %Y"),/FORNITURA DI .+\n/)) {
    pcv=search_num(@,/PCV euro\/cliente\/mese \N \N (\N)/);
    spesa_materia_energia=search_num(@,/DETTAGLIO DELLA SPESA PER (?:L\u2019ENERGIA ELETTRICA|LA MATERIA ENERGIA)[^]+?(\N) DETTAGLIO/);
    trasporto_gestione=search_num(@,/DETTAGLIO DELLA SPESA PER (?:IL TRASPORTO, GESTIONE DEL CONTATORE|TRASPORTO ENERGIA, GESTIONE CONTATORE) E ONERI DI SISTEMA[^]+?(\N) IMPOSTE/);

    (prezzo_energia[0], energia_attiva[0]) = calc_prezzo_energia(@, /(?:F1|Picco|Ore Giorno)/);
    (prezzo_energia[1], energia_attiva[1]) = calc_prezzo_energia(@, /(?:F2|Fuori Picco|Ore Notte)/);
    (prezzo_energia[2], energia_attiva[2]) = calc_prezzo_energia(@, /F3/);
    
    (prezzo_energia, energia_attiva) = calc_prezzo_energia(@, /(?:energia(?: monorario)?|Luce)/);

    sbilanciamento=search_num(@,/Incremento prezzo euro\/kWh (\N)/);

    imponibile=search_num(@,/FORNITURA DI.+ (\N)/);

    penale_reattiva_inf75=search_num(@,/Energia Reattiva tra il 33% e 75% attiva euro\/kVARh \N \N (\N)/);
    penale_reattiva_sup75=search_num(@,/Energia Reattiva oltre il 75% attiva euro\/kVARh \N \N (\N)/);

    potenza=repeated(search_num(@,/Quota Potenza euro\/kW\/mese \N (\N)/), 3);
    accise=search_num(@,/Imposta erariale.+ (\N)\n/);
  }
}
### End Script
### End Box
